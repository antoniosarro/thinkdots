This file is a merged representation of the entire codebase, combined into a single document by Repomix.

<file_summary>
This section contains a summary of this file.

<purpose>
This file contains a packed representation of the entire repository's contents.
It is designed to be easily consumable by AI systems for analysis, code review,
or other automated processes.
</purpose>

<file_format>
The content is organized as follows:
1. This summary section
2. Repository information
3. Directory structure
4. Repository files (if enabled)
5. Multiple file entries, each consisting of:
  - File path as an attribute
  - Full contents of the file
</file_format>

<usage_guidelines>
- This file should be treated as read-only. Any changes should be made to the
  original repository files, not this packed version.
- When processing this file, use the file path to distinguish
  between different files in the repository.
- Be aware that this file may contain sensitive information. Handle it with
  the same level of security as you would the original repository.
</usage_guidelines>

<notes>
- Some files may have been excluded based on .gitignore rules and Repomix's configuration
- Binary files are not included in this packed representation. Please refer to the Repository Structure section for a complete list of file paths, including binary files
- Files matching patterns in .gitignore are excluded
- Files matching default ignore patterns are excluded
- Files are sorted by Git change count (files with more changes are at the bottom)
</notes>

</file_summary>

<directory_structure>
home/
  antoniosarro/
    common/
      default.nix
      nixos.nix
    laptop.nix
  common/
    core/
      shell/
        bat.nix
        default.nix
        direnv.nix
        eza.nix
        fastfetch.nix
        fzf.nix
        lazygit.nix
        starship.nix
        tmux.nix
        zoxide.nix
        zsh.nix
      default.nix
      ghostty.nix
      git.nix
      kitty.nix
      nixos.nix
      xdg.nix
    optional/
      browsers/
        chromium.nix
        default.nix
        firefox.nix
        zen.nix
      comms/
        vesktop/
          themes/
            matte-black.css
          default.nix
        default.nix
      desktop/
        hyprland/
          hypr/
            bind.nix
            default.nix
            environment.nix
            input.nix
            monitor.nix
            style.nix
            windows.nix
            workspace.nix
          default.nix
          hypridle.nix
          hyprpaper.nix
          mako.nix
          waybar.nix
        default.nix
        gtk-qt.nix
        walker.nix
      development/
        default.nix
      media/
        spotify/
          default.nix
        default.nix
      tools/
        default.nix
hosts/
  common/
    core/
      default.nix
      nixos.nix
    disks/
      laptop.nix
    optional/
      services/
        bluetooth.nix
        greetd.nix
        logrotate.nix
        printing.nix
      audio.nix
      flatpak.nix
      fonts.nix
      gpu.nix
      graphene.nix
      hyprland.nix
      mpv.nix
      obsidian.nix
      plymouth.nix
      protonvpn.nix
      thunar.nix
      virtualisation.nix
      wifi.nix
    users/
      antoniosarro/
        nixos.nix
      default.nix
  nixos/
    laptop/
      default.nix
      hardware-configuration.nix
lib/
  default.nix
modules/
  common/
    default.nix
    host-spec.nix
    nix.nix
  home/
    default.nix
    monitors.nix
  hosts/
    common/
      default.nix
    nixos/
      default.nix
      warnings.nix
overlays/
  default.nix
pkgs/
  common/
    .gitkeep
wallpapers/
  1.jpg
checks.nix
flake.lock
flake.nix
shell.nix
</directory_structure>

<files>
This section contains the contents of the repository's files.

<file path="home/antoniosarro/common/default.nix">
{ ... }: { }
</file>

<file path="home/antoniosarro/common/nixos.nix">
{
  config,
  lib,
  ...
}:
let
  homeDirectory = config.home.homeDirectory;
in
{
  home = {
    sessionPath = lib.flatten ([
      "${homeDirectory}/scripts/"
    ]);
  };
}
</file>

<file path="home/antoniosarro/laptop.nix">
{ lib, config, ... }:
{
  imports = (
    map lib.custom.relativeToRoot (
      [
        # ============================
        # Hardware Required Configs
        # ============================
        "home/common/core"
        "home/common/core/nixos.nix"

        "home/antoniosarro/common/nixos.nix"
      ]
      ++
        # ============================
        # Host-specific Configs
        # ============================
        (map (f: "home/common/optional/${f}") [
          "browsers"
          "comms"
          "desktop"
          "development"
          "media"
          "tools"
        ])
    )
  );

  monitors = [
    {
      name = "eDP-1";
      width = 1920;
      height = 1080;
      refreshRate = 60;
      primary = true;
    }
  ];

  home.file."${config.home.homeDirectory}/media/images/wallpapers" = {
    recursive = true;
    source = lib.custom.relativeToRoot "wallpapers";
  };
}
</file>

<file path="home/common/core/shell/bat.nix">
{ pkgs, ... }:
{
  programs.bat = {
    enable = true;
    config = {
      style = "numbers,changes,header";
    };
    extraPackages = builtins.attrValues {
      inherit (pkgs.bat-extras)
        batgrep # search through and highlight files using ripgrep
        batdiff # diff a file against the current git index, or display the diff between to files
        batman # read manpages using bat as the formatter
        ;
    };
  };

  home.activation.batCacheRebuild = {
    after = [ "linkGeneration" ];
    before = [ ];
    data = ''
      ${pkgs.bat}/bin/bat cache --build
    '';
  };
}
</file>

<file path="home/common/core/shell/default.nix">
{ pkgs, ... }:
{
  imports = [
    # a cat clone with syntax highlighting and Git integration
    ./bat.nix

    # load and unload environment variables depending on the current directory
    ./direnv.nix

    # modern, maintained replacement for ls
    ./eza.nix

    # system information tool
    ./fastfetch.nix

    # command-line fuzzy finder written in Go
    ./fzf.nix

    # simple terminal UI for git commands
    ./lazygit.nix

    # minimal, blazing fast, and extremely customizable prompt for any shell
    ./starship.nix

    # terminal multiplexer
    ./tmux.nix

    # fast cd command that learns your habits
    ./zoxide.nix

    # z shell
    ./zsh.nix
  ];

  home.packages = builtins.attrValues {
    inherit (pkgs)
      btop
      curl
      wget
      ;
  };
}
</file>

<file path="home/common/core/shell/direnv.nix">
{
  programs.direnv = {
    enable = true;
    enableBashIntegration = true;
    enableZshIntegration = true;
    nix-direnv.enable = true;
  };
}
</file>

<file path="home/common/core/shell/eza.nix">
{
  programs.eza = {
    enable = true;
    icons = "auto";

    extraOptions = [
      "--group-directories-first"
      "--icons=always"
    ];
  };
}
</file>

<file path="home/common/core/shell/fastfetch.nix">
{
  programs.fastfetch = {
    enable = true;
    settings = {
      logo = {
        type = "builtin";
        height = 15;
        width = 30;
        padding = {
          top = 5;
          left = 3;
        };
      };
      modules = [
        { type = "break"; }
        {
          type = "custom";
          format = "{#30}┌──────────────────────Hardware──────────────────────┐";
        }
        {
          type = "host";
          key = " PC";
          keyColor = "green";
        }
        {
          type = "cpu";
          key = "│ ├";
          showPeCoreCount = true;
          keyColor = "green";
        }
        {
          type = "gpu";
          key = "│ ├";
          detectionMethod = "pci";
          keyColor = "green";
        }
        {
          type = "display";
          key = "│ ├󱄄";
          keyColor = "green";
        }
        {
          type = "disk";
          key = "│ ├󰋊";
          keyColor = "green";
        }
        {
          type = "memory";
          key = "│ ├";
          keyColor = "green";
        }
        {
          type = "swap";
          key = "└ └󰓡 ";
          keyColor = "green";
        }
        {
          type = "custom";
          format = "{#30}└────────────────────────────────────────────────────┘";
        }
        { type = "break"; }
        {
          type = "custom";
          format = "{#30}┌──────────────────────Software──────────────────────┐";
        }
        {
          type = "os";
          key = " OS";
          keyColor = "blue";
        }
        {
          type = "kernel";
          key = "│ ├";
          keyColor = "blue";
        }
        {
          type = "wm";
          key = "│ ├";
          keyColor = "blue";
        }
        {
          type = "de";
          key = " DE";
          keyColor = "blue";
        }
        {
          type = "terminal";
          key = "│ ├";
          keyColor = "blue";
        }
        {
          type = "packages";
          key = "│ ├󰏖";
          keyColor = "blue";
        }
        {
          type = "wmtheme";
          key = "│ ├󰉼";
          keyColor = "blue";
        }
        {
          type = "terminalfont";
          key = "└ └";
          keyColor = "blue";
        }
        {
          type = "custom";
          format = "{#30}└────────────────────────────────────────────────────┘";
        }
        { type = "break"; }
        {
          type = "custom";
          format = "{#30}┌────────────────────Uptime / Age────────────────────┐";
        }
        {
          type = "command";
          key = "󱦟 OS Age";
          keyColor = "magenta";
          text = "birth_install=$(stat -c %W /); current=$(date +%s); time_progression=$((current - birth_install)); days_difference=$((time_progression / 86400)); echo $days_difference days";
        }
        {
          type = "uptime";
          key = "󱫐 Uptime";
          keyColor = "magenta";
        }
        {
          type = "custom";
          format = "{#30}└────────────────────────────────────────────────────┘";
        }
        { type = "break"; }
      ];
    };
  };
}
</file>

<file path="home/common/core/shell/fzf.nix">
{
  programs.fzf = {
    enable = true;
    enableZshIntegration = true;
    colors = {
      "fg+" = "magenta";
      "bg+" = "-1";
      "fg" = "white";
      "bg" = "-1";
      "prompt" = "grey";
      "pointer" = "magenta";
    };
    defaultOptions = [
      "--margin=1"
      "--layout=reverse"
      "--border=rounded"
      "--info='hidden'"
      "--header=''"
      "--prompt='/ '"
      "-i"
      "--no-bold"
    ];
  };
}
</file>

<file path="home/common/core/shell/lazygit.nix">
{
  programs.lazygit = {
    enable = true;
    settings = {
      gui.theme = {
        ligthTheme = false;
        activeBorderColor = [
          "magenta"
          "bold"
        ];
        inactiveBorderColor = [ "black" ];
        selectedLineBgColor = [ "default" ];
      };
    };
  };
}
</file>

<file path="home/common/core/shell/starship.nix">
{ lib, ... }:
{
  programs.starship = {
    enable = true;
    enableZshIntegration = true;
    settings = {
      add_newline = true;
      format = "[$directory$git_branch$git_status]($style)$character";

      character = {
        success_symbol = "[❯](bold cyan)";
        error_symbol = "[✗](bold cyan)";
      };

      directory = {
        truncation_length = 2;
        truncation_symbol = "…/";
        repo_root_style = "bold cyan";
        repo_root_format = "[$repo_root]($repo_root_style)[$path]($style)[$read_only]($read_only_style) ";
      };

      git_branch = {
        format = "[$branch]($style) ";
        style = "italic cyan";
      };

      git_status = {
        format = "[$all_status]($style)";
        style = "cyan";
        ahead = "⇡\${count} ";
        diverged = "⇕⇡\${ahead_count\}⇣\${behind_count} ";
        behind = "⇣\${count} ";
        conflicted = " ";
        up_to_date = " ";
        untracked = "? ";
        modified = " ";
        stashed = "";
        staged = "";
        renamed = "";
        deleted = "";
      };
    };
  };
}
</file>

<file path="home/common/core/shell/tmux.nix">
{ pkgs, ... }:
{
  programs.tmux = {
    enable = true;
    plugins = with pkgs; [
      tmuxPlugins.vim-tmux-navigator
    ];
    extraConfig = ''
      set-option -g default-terminal 'tmux-256color'
      set-option -sa terminal-features ',xterm-kitty:RGB'
    '';
  };
}
</file>

<file path="home/common/core/shell/zoxide.nix">
{
  programs.zoxide = {
    enable = true;
    enableBashIntegration = true;
    enableZshIntegration = true;
  };
}
</file>

<file path="home/common/core/shell/zsh.nix">
{
  programs.zsh = {
    enable = true;

    dotDir = ".config/zsh";
    enableCompletion = true;
    syntaxHighlighting.enable = true;
    autocd = true;
    autosuggestion.enable = true;

    history = {
      size = 10000;
      share = true;
      ignoreAllDups = true;
      ignoreSpace = true;
    };

    shellAliases = {
      #-------------Bat related------------
      cat = "bat --paging=never";
      diff = "batdiff";
      rg = "batgrep";
      man = "batman";

      #------------Navigation------------
      l = "eza -lah";
      la = "eza -lah";
      ll = "eza -lh";
      ls = "eza";
      lsa = "eza -lah";

      #-------------Neovim---------------
      e = "nvim";
      vi = "nvim";
      vim = "nvim";

      #-------------SSH---------------
      ssh = "TERM=xterm-256color ssh";
    };
  };
}
</file>

<file path="home/common/core/default.nix">
{
  config,
  lib,
  pkgs,
  hostSpec,
  ...
}:
let
  platform = "nixos";
in
{
  imports = lib.flatten [
    (map lib.custom.relativeToRoot [
      "modules/common/host-spec.nix"
      "modules/home"
    ])

    ./${platform}.nix

    ./shell

    ./ghostty.nix
    ./git.nix
    ./kitty.nix
    ./xdg.nix
  ];

  inherit hostSpec;

  home = {
    username = lib.mkDefault config.hostSpec.username;
    homeDirectory = lib.mkDefault config.hostSpec.home;
    stateVersion = lib.mkDefault "25.05";
    sessionPath = [
      "$HOME/.local/bin"
    ];
    sessionVariables = {
      FLAKE = "$HOME/thinkdots";
      SHELL = "zsh";
      TERM = "kitty";
      TERMINAL = "kitty";
      VISUAL = "nvim";
      EDITOR = "nvim";
      MANPAGER = "batman";
    };
    preferXdgDirectories = true;
  };

  home.packages = builtins.attrValues {
    inherit (pkgs)
      # TODO
      ;
  };

  nix = {
    package = lib.mkDefault pkgs.nix;
    settings = {
      experimental-features = [
        "nix-command"
        "flakes"
      ];
      warn-dirty = false;
    };
  };

  programs.home-manager.enable = true;

  systemd.user.startServices = "sd-switch";
}
</file>

<file path="home/common/core/ghostty.nix">
{ pkgs, ... }:
{
  programs.ghostty = {
    enable = true;
    package = pkgs.unstable.ghostty;
    settings = {
      scrollback-limit = 10000;
      #NOTE(ghostty): not using ghostty for splits or tabs so nearly all default binds conflict Hypr, nvim, or zellij
      keybind = [
        "ctrl+shift+d=inspector:toggle"
        "ctrl+shift+c=copy_to_clipboard"
        "ctrl+shift+v=paste_from_clipboard"
        # Fix fixterm conflict with zsh ^[ character https://github.com/ghostty-org/ghostty/discussions/5071
        "ctrl+left_bracket=text:\\x1b"
        "ctrl+shift+minus=decrease_font_size:1"
        "ctrl+shift+plus=increase_font_size:1"
        "ctrl+shift+0=reset_font_size"
        #
        # ========== UNBIND ==========
        #
        "ctrl+shift+e=unbind" # new_split
        "ctrl+shift+n=unbind" # new_window
        "ctrl+shift+t=unbind" # new_tab
      ];
    };
  };
}
</file>

<file path="home/common/core/git.nix">
{
  pkgs,
  config,
  ...
}:
let
  handle = config.hostSpec.handle;
  publicGitEmail = config.hostSpec.email.github;
in
{
  programs.git = {
    enable = true;
    package = pkgs.gitAndTools.gitFull;

    ignores = [
      "*.drv"
      "result"
      "*.py?"
      "__pycache__/"
      ".venv/"
      ".direnv"
      "node_modules"
      ".cache/"
      ".DS_Store"
    ];

    userName = handle;
    userEmail = publicGitEmail;
    aliases = { };
    extraConfig = {
      log.showSignature = "true";
      init.defaultBranch = "main";
      pull.rebase = "true";
      push.autoSetupRemote = true;
    };
  };
}
</file>

<file path="home/common/core/kitty.nix">
{
  programs.kitty = {
    enable = true;
    shellIntegration.enableZshIntegration = true;

    settings = {
      scrollback_lines = 10000;

      # Font
      font_family = "Iosevka Nerd Font Mono";
      font_size = 9;
      bold_italic_font = "auto";

      # Window
      window_padding_width = 14;
      window_padding_height = 14;
      hide_window_decorations = "yes";
      show_window_resize_notification = "no";
      confirm_os_window_close = 0;

      # Aesthetics
      cursor_shape = "block";
      enable_audio_bell = "no";

      # Minimal Tab bar styling
      tab_bar_edge = "bottom";
      tab_bar_style = "powerline";
      tab_powerline_style = "slanted";
      tab_title_template = "{title}{' :{}:'.format(num_windows) if num_windows > 1 else ''}";

      # Theme
      foreground = "#bebebe";
      background = "#121212";
      selection_foreground = "#121212";
      selection_background = "#333333";

      cursor = "#eaeaea";
      cursor_text_color = "#121212";

      url_color = "#bebebe";

      active_border_color = "#595959";
      inactive_border_color = "#595959";
      bell_border_color = "#595959";

      wayland_titlebar_color = "system";
      macos_titlebar_color = "system";

      active_tab_foreground = "#bebebe";
      active_tab_background = "#121212";
      inactive_tab_foreground = "#bebebe";
      inactive_tab_background = "#121212";
      tab_bar_background = "#bebebe";

      mark1_foreground = "#121212";
      mark1_background = "#404040";
      mark2_foreground = "#121212";
      mark2_background = "#121212";
      mark3_foreground = "#121212";
      mark3_background = "#a6a6a6";

      color0 = "#333333";
      color8 = "#8a8a8d";
      color1 = "#D35F5F";
      color9 = "#B91C1C";
      color2 = "#FFC107";
      color10 = "#FFC107";
      color3 = "#B91C1C";
      color11 = "#b90a0a";
      color4 = "#e68e0d";
      color12 = "#f59e0b";
      color5 = "#D35F5F";
      color13 = "#B91C1C";
      color6 = "#bebebe";
      color14 = "#eaeaea";
      color7 = "#bebebe";
      color15 = "#ffffff";
    };
  };
}
</file>

<file path="home/common/core/nixos.nix">
{
  config,
  pkgs,
  lib,
  ...
}:
{
  imports = [

  ];

  home = {
    packages = lib.optionals (config.hostSpec.isProduction) (
      builtins.attrValues {
        inherit (pkgs)
          ;
      }
    );

    activation.reloadFontCache = lib.hm.dag.entryAfter [ "linkActivation" ] ''
      if [ -x "${pkgs.fontconfig}/bin/fc-cache" ]; then
        ${pkgs.fontconfig}/bin/fc-cache -f
      fi
    '';

    sessionVariables = {

    }
    // lib.optionalAttrs config.hostSpec.useWayland {
      QT_QPA_PLATFORM = "wayland";
      GDK_BACKEND = "wayland";
      CLUTTER_BACKEND = "wayland"; # for gnome-shell
      SDL_VIDEODRIVER = "wayland"; # for SDL apps
      NIXOS_OZONE_WL = "1"; # for chromium, vscode, electron, etc
      XDG_SESSION_TYPE = "wayland";
      MOZ_ENABLE_WAYLAND = "1";

    };
  };

  services.ssh-agent.enable = true;
}
</file>

<file path="home/common/core/xdg.nix">
{
  config,
  pkgs,
  ...
}:
{
  xdg = {
    enable = true;
    userDirs = {
      enable = true;
      createDirectories = true;
      desktop = "${config.home.homeDirectory}/.desktop";
      documents = "${config.home.homeDirectory}/documents";
      download = "${config.home.homeDirectory}/downloads";
      music = "${config.home.homeDirectory}/media/audio";
      pictures = "${config.home.homeDirectory}/media/images";
      videos = "${config.home.homeDirectory}/media/video";

      extraConfig = {
        XDG_PUBLICSHARE_DIR = "/var/empty";
        XDG_TEMPLATES_DIR = "/var/empty";
      };
    };
  };

  home.packages = builtins.attrValues {
    inherit (pkgs)
      xdg-utils
      xdg-user-dirs
      ;
  };
}
</file>

<file path="home/common/optional/browsers/chromium.nix">
{
  programs.chromium = {
    enable = true;
    commandLineArgs = [
      "--no-default-browser-check"
      "--restore-last-session"
    ];
  };
}
</file>

<file path="home/common/optional/browsers/default.nix">
{
  pkgs,
  inputs,
  ...
}:
{
  imports = [
    ./chromium.nix
    ./firefox.nix
    ./zen.nix
  ];
}
</file>

<file path="home/common/optional/browsers/firefox.nix">
{ config, ... }:
let
  homeDir = config.home.homeDirectory;
in
{
  programs.firefox = {
    enable = true;

    # Refer to https://mozilla.github.io/policy-templates or `about:policies#documentation` in firefox
    policies = {
      AppAutoUpdate = false; # disable automatic application update
      BackgroundAppUpdate = false; # disable automatic application update in the background, when the application is not running
      DefaultDownloadDirectory = "${config.home.homeDirectory}/downloads";
      DisableBuiltinPDFViewer = false;
      DisableFirefoxStudies = true;
      DisableFirefoxAccounts = false; # Enable Firefox Sync
      DisablePocket = true;
      DisableTelemetry = true;
      DontCheckDefaultBrowser = true;
      OfferToSaveLogins = false;
      EnableTrackingProtection = {
        Value = true;
        Locked = true;
        Cryptomining = true;
        Fingerprinting = true;
        EmailTracking = true;
      };
      ExtensionUpdate = false;

      ExtensionSettings =
        (
          let
            extension = shortId: uuid: {
              name = uuid;
              value = {
                install_url = "https://addons.mozilla.org/en-US/firefox/downloads/latest/${shortId}/latest.xpi";
                installation_mode = "normal_installed";
              };
            };
          in
          builtins.listToAttrs [
            # privacy / Security
            (extension "noscript" "{73a6fe31-595d-460b-a920-fcc0f8843232}")
            (extension "proton-vpn-firefox-extension" "vpn@proton.ch")
            (extension "privacy-badger17" "jid1-MnnxcxisBPnSXQ@jetpack")
            (extension "cookie-autodelete" "CookieAutoDelete@kennydo.com")
            (extension "clearurls" "{74145f27-f039-47ce-a470-a662b129930a}")

            # adblock
            (extension "ublock-origin" "uBlock0@raymondhill.net")
            # https://github.com/AdguardTeam/AdGuardExtra

            # layout / Themeing
            (extension "darkreader" "addon@darkreader.org")
            (extension "adaptive-tab-bar-colour" "ATBC@EasonWong")
            (extension "refined-github-" "{a4c4eda4-fb84-4a84-b4a1-f7c1cbf2a1ad}")

            # misc
            (extension "auto-tab-discard" "{c2c003ee-bd69-42a2-b0e9-6f34222cb046}")
            (extension "betterttv" "firefox@betterttv.net")
            (extension "keepa" "amptra@keepa.com")
            (extension "multi-account-containers" "@testpilot-containers")
            (extension "open-graph-previewer" "ruben.winant@hotmail.com")
          ]
        )
        // {
        };
    };

    profiles.main = {
      id = 0;
      name = "ohhbigg";
      isDefault = true;
      settings = {
        "signon.rememberSignons" = false; # disable built-in password manager
        "browser.compactmode.show" = true;
        "browser.uidensity" = 1; # enable compact mode
        "browser.aboutConfig.showWarning" = false;
        "browser.download.dir" = "${homeDir}/downloads";
        "browser.tabs.firefox-view" = true; # sync tabs across devices
        "ui.systemUsesDarkTheme" = 1; # force dark theme
        "extensions.pocket.enabled" = false;
      };
    };
  };

  xdg.mimeApps = {
    enable = true;
    defaultApplications = {
      "text/html" = [ "firefox.desktop" ];
      "text/xml" = [ "firefox.desktop" ];
      "application/pdf" = [ "firefox.desktop" ];
      "x-scheme-handler/http" = [ "firefox.desktop" ];
      "x-scheme-handler/https" = [ "firefox.desktop" ];
    };
  };
}
</file>

<file path="home/common/optional/browsers/zen.nix">
{
  inputs,
  ...
}:
{
  imports = [
    inputs.zen-browser.homeModules.beta
  ];

  programs.zen-browser = {
    enable = true;
  };
}
</file>

<file path="home/common/optional/comms/vesktop/default.nix">
{
  pkgs,
  config,
  ...
}:
{
  home.packages = builtins.attrValues {
    inherit (pkgs)
      vesktop
      ;
  };

  home.file."${config.home.homeDirectory}/.config/vesktop/themes" = {
    source = ./themes;
    recursive = true;
  };
}
</file>

<file path="home/common/optional/comms/default.nix">
{ pkgs, ... }:
{
  imports = [
    ./vesktop
  ];

  home.packages = builtins.attrValues {
    inherit (pkgs)

      ;

    inherit (pkgs.unstable)
      teams-for-linux
      telegram-desktop
      ;
  };
}
</file>

<file path="home/common/optional/desktop/hyprland/hypr/bind.nix">
{ pkgs, ... }:
{
  wayland.windowManager.hyprland.settings = {
    # ============================
    # Main Key
    # ============================
    "$mainMod" = "SUPER";

    # ============================
    # Application bindings
    # ============================
    "$terminal" = "$TERMINAL";
    "$browser" = "nixdots-launch-browser";

    # ============================
    # Media Binds
    # ============================
    "$osdclient" =
      ''swayosd-client --monitor "$(hyprctl monitors -j | jq -r '.[] | select(.focused == true).name')"'';

    bindeld = [
      # Laptop multimedia keys for volume and LCD brightness (with OSD)
      ",XF86AudioRaiseVolume, Volume up, exec, $osdclient --output-volume raise"
      ",XF86AudioLowerVolume, Volume down, exec, $osdclient --output-volume lower"
      ",XF86AudioMute, Mute, exec, $osdclient --output-volume mute-toggle"
      ",XF86AudioMicMute, Mute microphone, exec, $osdclient --input-volume mute-toggle"
      ",XF86MonBrightnessUp, Brightness up, exec, $osdclient --brightness raise"
      ",XF86MonBrightnessDown, Brightness down, exec, $osdclient --brightness lower"

      # Precise 1% multimedia adjustments with Alt modifier
      "ALT, XF86AudioRaiseVolume, Volume up precise, exec, $osdclient --output-volume +1"
      "ALT, XF86AudioLowerVolume, Volume down precise, exec, $osdclient --output-volume -1"
      "ALT, XF86MonBrightnessUp, Brightness up precise, exec, $osdclient --brightness +1"
      "ALT, XF86MonBrightnessDown, Brightness down precise, exec, $osdclient --brightness -1"

      # Requires playerctl
      ",XF86AudioNext, Next track, exec, $osdclient --playerctl next"
      ",XF86AudioPause, Pause, exec, $osdclient --playerctl play-pause"
      ",XF86AudioPlay, Play, exec, $osdclient --playerctl play-pause"
      ",XF86AudioPrev, Previous track, exec, $osdclient --playerctl previous"
    ];

    bindld = [
      # Switch audio output with Super + Mute
      "SUPER, XF86AudioMute, Switch audio output, exec, nixdots-cmd-audio-switch"
    ];

    bindd = [
      "SUPER, Return, Terminal, exec, $terminal"
      "SUPER, F, File manager, exec, thunar --window"
      "SUPER, B, Browser, exec, $browser"
      "SUPER SHIFT, B, Browser (private), exec, $browser --private"
      "SUPER, M, Music, exec, nixdots-launch-or-focus spotify"
      "SUPER, N, Editor, exec, nixdots-launch-editor"
      "SUPER, T, Activity, exec, $terminal -e btop"
      "SUPER, D, Docker, exec, $terminal -e lazydocker"
      ''SUPER, G, Signal, exec, omarchy-launch-or-focus signal "signal-desktop"''
      ''SUPER, O, Obsidian, exec, omarchy-launch-or-focus obsidian "obsidian -disable-gpu --enable-wayland-ime"''
      "SUPER, slash, Passwords, exec, bitwarden"

      # Menus
      ''SUPER, SPACE, Launch apps, exec, walker -p "Start"''
      "SUPER CTRL, E, Emoji picker, exec, walker -m Emojis"
      "SUPER ALT, SPACE, Nixdots menu, exec, nixdots-menu"
      "SUPER, ESCAPE, Power menu, exec, nixdots-menu system"
      ", XF86PowerOff, Power menu, exec, nixdots-menu system"
      "SUPER, K, Show key bindings, exec, nixdots-menu-keybindings"

      # Aesthetics
      "SUPER SHIFT, SPACE, Toggle top bar, exec, nixdots-toggle-waybar"
      ''SUPER, BACKSPACE, Toggle window transparency, exec, hyprctl dispatch setprop "address:$(hyprctl activewindow -j | jq -r '.address')" opaque toggle''

      # Notifications
      "SUPER, COMMA, Dismiss last notification, exec, makoctl dismiss"
      "SUPER SHIFT, COMMA, Dismiss all notifications, exec, makoctl dismiss --all"
      ''SUPER CTRL, COMMA, Toggle silencing notifications, exec, makoctl mode -t do-not-disturb && makoctl mode | grep -q 'do-not-disturb' && notify-send "Silenced notifications" || notify-send "Enabled notifications"''

      # Toggle idling
      "SUPER CTRL, I, Toggle locking on idle, exec, nixdots-toggle-idle"

      # Toggle nightlight
      "SUPER CTRL, N, Toggle nightlight, exec, nixdots-toggle-nightlight"

      # Screenshots
      ", PRINT, Screenshot of region, exec, nixdots-cmd-screenshot"
      "SHIFT, PRINT, Screenshot of window, exec, nixdots-cmd-screenshot window"
      "CTRL, PRINT, Screenshot of display, exec, nixdots-cmd-screenshot output"

      # Screen recordings
      "ALT, PRINT, Screen record a region, exec, nixdots-cmd-screenrecord region"
      "ALT SHIFT, PRINT, Screen record a region with audio, exec, nixdots-cmd-screenrecord region audio"
      "CTRL ALT, PRINT, Screen record display, exec, nixdots-cmd-screenrecord output"
      "CTRL ALT SHIFT, PRINT, Screen record display with audio, exec, nixdots-cmd-screenrecord output audio"

      # Color picker
      "SUPER, PRINT, Color picker, exec, pkill hyprpicker || hyprpicker -a"

      # File sharing
      "CTRL SUPER, S, Share, exec, nixdots-menu share"

      # Close windows
      "SUPER, W, Close active window, killactive"
      "CTRL ALT, DELETE, Close all Windows, exec, nixdots-cmd-close-all-windows"

      # Control tiling
      "SUPER, J, Toggle split, togglesplit" # dwindle
      "SUPER, P, Pseudo window, pseudo" # dwindle
      "SUPER, V, Toggle floating, togglefloating"
      "SHIFT, F11, Force full screen, fullscreen, 0"
      "ALT, F11, Full width, fullscreen, 1"

      # Move focus with SUPER + arrow keys
      "SUPER, left, Move focus left, movefocus, l"
      "SUPER, right, Move focus right, movefocus, r"
      "SUPER, up, Move focus up, movefocus, u"
      "SUPER, down, Move focus down, movefocus, d"

      # Tab between workspaces
      "SUPER, TAB, Next workspace, workspace, e+1"
      "SUPER SHIFT, TAB, Previous workspace, workspace, e-1"
      "SUPER CTRL, TAB, Former workspace, workspace, previous"

      # Swap active window with the one next to it with SUPER + SHIFT + arrow keys
      "SUPER SHIFT, left, Swap window to the left, swapwindow, l"
      "SUPER SHIFT, right, Swap window to the right, swapwindow, r"
      "SUPER SHIFT, up, Swap window up, swapwindow, u"
      "SUPER SHIFT, down, Swap window down, swapwindow, d"

      # Cycle through applications on active workspace
      "ALT, Tab, Cycle to next window, cyclenext"
      "ALT SHIFT, Tab, Cycle to prev window, cyclenext, prev"
      "ALT, Tab, Reveal active window on top, bringactivetotop"
      "ALT SHIFT, Tab, Reveal active window on top, bringactivetotop"

      # Resize active window
      "SUPER, code:20, Expand window left, resizeactive, -100 0"
      "SUPER, code:21, Shrink window left, resizeactive, 100 0"
      "SUPER SHIFT, code:20, Shrink window up, resizeactive, 0 -100"
      "SUPER SHIFT, code:21, Expand window down, resizeactive, 0 100"

      # Scroll through existing workspaces with SUPER + scroll
      "SUPER, mouse_down, Scroll active workspace forward, workspace, e+1"
      "SUPER, mouse_up, Scroll active workspace backward, workspace, e-1"
    ]
    # Dynamically generate workspace switching keybinds (SUPER + [1-0])
    ++ (builtins.genList (
      i:
      let
        ws = i + 1;
        key = if ws == 10 then 0 else ws;
        keycode = toString (9 + ws); # code:10 through code:19
      in
      "SUPER, code:${keycode}, Switch to workspace ${toString ws}, workspace, ${toString ws}"
    ) 10)
    # Dynamically generate move to workspace keybinds (SUPER + SHIFT + [1-0])
    ++ (builtins.genList (
      i:
      let
        ws = i + 1;
        key = if ws == 10 then 0 else ws;
        keycode = toString (9 + ws); # code:10 through code:19
      in
      "SUPER SHIFT, code:${keycode}, Move window to workspace ${toString ws}, movetoworkspace, ${toString ws}"
    ) 10);

    bindmd = [
      # Move/resize windows with mainMod + LMB/RMB and dragging
      "SUPER, mouse:272, Move window, movewindow"
      "SUPER, mouse:273, Resize window, resizewindow"
    ];
  };

  home.packages = with pkgs; [
    swayosd
    jq
    playerctl
    hyprpicker
  ];
}
</file>

<file path="home/common/optional/desktop/hyprland/hypr/default.nix">
{
  imports = [
    ./bind.nix
    ./environment.nix
    ./input.nix
    ./monitor.nix
    ./style.nix
    ./workspace.nix
  ];
}
</file>

<file path="home/common/optional/desktop/hyprland/hypr/environment.nix">
{
  wayland.windowManager.hyprland.settings = {
    env = [
      # Cursor size
      "XCURSOR_SIZE,24"
      "HYPRCURSOR_SIZE,24"

      # XDG Specifications
      "XDG_SESSION_TYPE,wayland"

      # Qt Variables
      "QT_AUTO_SCREEN_SCALE_FACTOR,1"

      # GDK Variables
      "GDK_BACKEND,wayland,x11,*"
      "GDK_SCALE,1.25"
      "GDK_DPI_SCALE,0.8"

      # Toolkit Backend Variables
      "QT_QPA_PLATFORM,wayland;xcb"
      "QT_STYLE_OVERRIDE,kvantum"

      # Hyprcursor
      "HYPRCURSOR_THEME,rose-pine-hyprcursor"

      # Force all apps to use Wayland
      "SDL_VIDEODRIVER,wayland"
      "OZONE_PLATFORM,wayland"

      # Allow better support for screen sharing
      "XDG_CURRENT_DESKTOP,Hyprland"
      "XDG_SESSION_DESKTOP,Hyprland"

      # Electron stuff
      "NIXOS_OZONE_WL, 1"
      "ELECTRON_OZONE_PLATFORM_HINT,wayland"

      # Firefox stuff
      "MOZ_ENABLE_WAYLAND, 1"
      "MOZ_WEBRENDER, 1"


      # Use XCompose file
      "XCOMPOSEFILE,~/.XCompose"
    ];

    xwayland = {
      force_zero_scaling = false;
    };

    ecosystem = {
      no_update_news = true;
    };
  };
}
</file>

<file path="home/common/optional/desktop/hyprland/hypr/input.nix">
{
  wayland.windowManager.hyprland.settings = {
    input = {
      kb_layout = "us";
      kb_variant = "";
      kb_model = "";
      kb_options = "compose:caps";

      repeat_rate = 40;
      repeat_delay = 600;

      follow_mouse = 1;

      sensitivity = 0;

      touchpad = {
        natural_scroll = false;
        scroll_factor = 0.4;
      };
    };
  };
}
</file>

<file path="home/common/optional/desktop/hyprland/hypr/monitor.nix">
{ config, ... }:
{
  wayland.windowManager.hyprland.settings = {
    monitor = (
      map (
        m:
        "${m.name},${
          if m.enabled then
            "${toString m.width}x${toString m.height}@${toString m.refreshRate},${toString m.x}x${toString m.y},1,transform,${toString m.transform},vrr,${toString m.vrr}"
          else
            "disable"
        }"
      ) (config.monitors)
    );
  };
}
</file>

<file path="home/common/optional/desktop/hyprland/hypr/style.nix">
{
  wayland.windowManager.hyprland.settings = {
    general = {
      gaps_in = 5;
      gaps_out = 10;
      border_size = 2;

      "col.active_border" = "rgb(8A8A8D)";
      "col.inactive_border" = "rgba(595959aa)";

      resize_on_border = false;
      allow_tearing = false;
      layout = "dwindle";
    };

    decoration = {
      rounding = 0;

      shadow = {
        enabled = true;
        range = 2;
        render_power = 3;
        color = "rgba(1a1a1aee)";
      };

      blur = {
        enabled = false;
        size = 3;
        passes = 1;
        vibrancy = 0.1696;
      };
    };

    animations = {
      enabled = true;

      bezier = [
        "easeOutQuint,0.23,1,0.32,1"
        "easeInOutCubic,0.65,0.05,0.36,1"
        "linear,0,0,1,1"
        "almostLinear,0.5,0.5,0.75,1.0"
        "quick,0.15,0,0.1,1"
      ];

      animation = [
        "global, 1, 10, default"
        "border, 1, 5.39, easeOutQuint"
        "windows, 1, 4.79, easeOutQuint"
        "windowsIn, 1, 4.1, easeOutQuint, popin 87%"
        "windowsOut, 1, 1.49, linear, popin 87%"
        "fadeIn, 1, 1.73, almostLinear"
        "fadeOut, 1, 1.46, almostLinear"
        "fade, 1, 3.03, quick"
        "layers, 1, 3.81, easeOutQuint"
        "layersIn, 1, 4, easeOutQuint, fade"
        "layersOut, 1, 1.5, linear, fade"
        "fadeLayersIn, 1, 1.79, almostLinear"
        "fadeLayersOut, 1, 1.39, almostLinear"
        "workspaces, 0, 0, ease"
      ];
    };

    dwindle = {
      pseudotile = true;
      preserve_split = true;
      force_split = 2;
    };

    master = {
      new_status = "master";
    };

    misc = {
      disable_hyprland_logo = true;
      disable_splash_rendering = true;
      focus_on_activate = true;
    };
  };
}
</file>

<file path="home/common/optional/desktop/hyprland/hypr/windows.nix">
{
  wayland.windowManager.hyprland.settings = {
    windowrule = [
      "suppressevent maximize, class:.*"
      "opacity 0.97 0.9, class:.*"
      "nofocus,class:^$,title:^$,xwayland:1,floating:1,fullscreen:0,pinned:0"

      # Bitwarden
      "noscreenshare, class:^(Bitwarden)$"

      # Browsers
      "tag +chromium-based-browser, class:([cC]hrom(e|ium)|[bB]rave-browser|Microsoft-edge|Vivaldi-stable)"
      "tag +firefox-based-browser, class:([fF]irefox|zen|librewolf)"

      # Force chromium-based browsers into a tile to deal with --app bug
      "tile, tag:chromium-based-browser"

      # Only a subtle opacity change, but not for video sites
      "opacity 1 0.97, tag:chromium-based-browser"
      "opacity 1 0.97, tag:firefox-based-browser"

      # Some video sites should never have opacity applied to them
      "opacity 1.0 1.0, initialTitle:((?i)(?:[a-z0-9-]+\.)*youtube\.com_/|app\.zoom\.us_/wc/home)"

      # Float LocalSend and fzf file picker
      "float, class:(Share|localsend)"
      "center, class:(Share|localsend)"

      # Picture-in-picture overlays
      "tag +pip, title:(Picture.{0,1}in.{0,1}[Pp]icture)"
      "float, tag:pip"
      "pin, tag:pip"
      "size 600 338, tag:pip"
      "keepaspectratio, tag:pip"
      "noborder, tag:pip"
      "opacity 1 1, tag:pip"
      "move 100%-w-40 4%, tag:pip"

      # Qemu
      "opacity 1 1, class:qemu"

      # Steam
      "float, class:steam"
      "center, class:steam, title:Steam"
      "opacity 1 1, class:steam"
      "size 1100 700, class:steam, title:Steam"
      "size 460 800, class:steam, title:Friends List"
      "idleinhibit fullscreen, class:steam"

      # Floating windows
      "float, tag:floating-window"
      "center, tag:floating-window"
      "size 800 600, tag:floating-window"

      "tag +floating-window, class:(blueberry.py|Impala|Wiremix|com.gabm.satty|About)"
      "tag +floating-window, class:(xdg-desktop-portal-hyprland|DesktopEditors), title:^(Open.*Files?|Open Folder|Save.*Files?|Save.*As|Save|All Files)"

      # Fullscreen screensaver
      "fullscreen, class:Screensaver"

      # No transparency on media windows
      "opacity 1 1, class:^(mpv|com.obsproject.Studio|imv)$"

      # Define terminal tag to style them uniformly
      "tag +terminal, class:(Alacritty|kitty|com.mitchellh.ghostty)"

      # Scroll faster in the terminal
      "scrolltouchpad 1.5, tag:terminal"
    ];

    layerrule = [
      # Remove 1px border around hyprshot screenshots
      "noanim, selection"

      # Application-specific animation
      "noanim, walker"
    ];
  };
}
</file>

<file path="home/common/optional/desktop/hyprland/hypr/workspace.nix">
{
  config,
  lib,
  ...
}:
{
  wayland.windowManager.hyprland.settings = {
    workspace = (
      let
        workspaceIDs = lib.flatten [
          (lib.range 1 10)
          "special"
        ];
        isPrimary = x: x ? primary && x.primary;
        primary = lib.lists.findFirst isPrimary { } config.monitors;
      in
      # workspace structure "[workspace], monitor:[name], default:[bool], persistent:[bool]"
      map (
        id:
        let
          id_as_string = toString id;
          # determine if the monitor is intended to display a specific workspace
          monitor = lib.lists.findFirst (
            x: x ? "workspace" && id_as_string == x.workspace
          ) primary config.monitors;
          # workspace 1 and any workspaces specific to the non-primary monitors are persistent
          persistent = if (id == 1 || !(isPrimary monitor)) then ", persistent:true" else "";
        in
        "${id_as_string}, monitor:${monitor.name}, default:true" + persistent
      ) workspaceIDs
    );
  };
}
</file>

<file path="home/common/optional/desktop/hyprland/default.nix">
{
  lib,
  pkgs,
  ...
}:
{
  imports = [
    ./hypr
    ./hypridle.nix
    ./hyprpaper.nix
  ];

  wayland.windowManager.hyprland = {
    enable = true;
    package = pkgs.unstable.hyprland;
    systemd = {
      variables = ["--all"];
    };
    settings = {
      exec-once = [
        "mako"
        "swayosd-server"
        "walker --gapplication-service &"
        "${pkgs.polkit_gnome}/libexec/polkit-gnome-authentication-agent-1"
        "wl-clip-persist --clipboard regular --all-mime-type-regex '^(?!x-kde-passwordManagerHint).+'"
      ];
    };
  };
}
</file>

<file path="home/common/optional/desktop/hyprland/hypridle.nix">
{ config, lib, ... }:
{
  services.hypridle = {
    enable = true;
    settings = {
      general = {
        lock_cmd = "nixdots-lock-screen";
        before_sleep_cmd = "loginctl lock-session";
        after_sleep_cmd = "hyprctl dispatch dpms on";
        inhibit_sleep = 3;
      };

      listener = [
        {
          timeout = 150;
          on-timeout = "pidof hyprlock || nixdots-launch-screensaver";
        }
        {
          timeout = 300;
          on-timeout = "loginctl lock-session";
        }
        {
          timeout = 330;
          on-timeout = "hyprctl dispatch dpms off";
          on-resume = "hyprctl dispatch dpms on";
        }
      ];
    };
  };
  systemd.user.services.hyprpaper.Unit.After = lib.mkForce [ "graphical-session.target" ];
}
</file>

<file path="home/common/optional/desktop/hyprland/hyprpaper.nix">
{ config, lib, ... }:
{
  services.hyprpaper = {
    enable = true;
    settings = {
      preload = [ "${config.home.homeDirectory}/media/images/wallpapers/1.jpg" ];
      wallpaper = [ ",${config.home.homeDirectory}/media/images/wallpapers/1.jpg" ];
    };
  };
  systemd.user.services.hyprpaper.Unit.After = lib.mkForce [ "graphical-session.target" ];
}
</file>

<file path="home/common/optional/desktop/hyprland/mako.nix">
{ config, ... }:
{
  services.mako = {
    enable = true;
    settings = {
      anchor = "top-right";
      default-timeout = 5000;
      width = 420;
      height = 110;
    };
    extraConfig = ''
      [app-name=Spotify]
      invisible=1

      [mode=do-not-disturb]
      invisible=true

      [mode=do-not-disturb app-name=notify-send]
      invisible=false

      [urgency=critical]
      default-timeout=0
    '';
  };
}
</file>

<file path="home/common/optional/desktop/hyprland/waybar.nix">
{ config, ... }:
{
  systemd.user.services.waybar = {
    Unit.StartLimitInterval = 0;
    #Unit.StartLimitBurst = 30;
  };

  programs.waybar = {
    enable = true;
    systemd = {
      enable = true;
    };
    settings = {
      mainBar = {
        layer = "top";
        position = "top";
        spacing = 0;
        height = 26;
        modules-left = [
          "custom/nixdots"
          "hyprland/workspaces"
        ];
        modules-center = [
          "clock"
          "custom/screenrecording-indicator"
        ];
        modules-right = [
          "group/tray-expander"
          "bluetooth"
          "network"
          "pulseaudio"
          "cpu"
        ];

        # ============================
        # Module Left
        # ============================
        "custom/nixdots" = {
          "format" = "<span>\ue843</span>";
          "on-click" = "nixdots-menu";
          "tooltip-format" = "Omarchy Menu\n\nSuper + Alt + Space";
        };

        "hyprland/workspaces" = {
          "on-click" = "activate";
          "format" = "{icon}";
          "format-icons" = {
            "default" = "";
            "1" = "1";
            "2" = "2";
            "3" = "3";
            "4" = "4";
            "5" = "5";
            "6" = "6";
            "7" = "7";
            "8" = "8";
            "9" = "9";
            "active" = "󱓻";
          };
          "persistent-workspaces" = {
            "1" = "[]";
            "2" = "[]";
            "3" = "[]";
            "4" = "[]";
            "5" = "[]";
          };
        };

        # ============================
        # Module Center
        # ============================
        "clock" = {
          "format" = "{:L%A %H:%M}";
          "format-alt" = "{:L%d %B W%V %Y}";
          "tooltip" = false;
          "on-click-right" = "nixdots-cmd-tzupdate";
        };

        "custom/screenrecording-indicator" = {
          "on-click" = "nixdots-cmd-screenrecord";
          "signal" = 8;
          "return-type" = "json";
          "exec" = "nixdots-waybar-screen-recording";
        };

        # ============================
        # Module Right
        # ============================
        "group/tray-expander" = {
          "orientation" = "inherit";
          "drawer" = {
            "transition-duration" = 600;
            "children-class" = "tray-group-item";
          };
          "modules" = [
            "custom/expand-icon"
            "tray"
          ];
        };
        "custom/expand-icon" = {
          "format" = " ";
          "tooltip" = false;
        };
        "tray" = {
          "icon-size" = 12;
          "spacing" = 12;
        };

        "bluetooth" = {
          "format" = "";
          "format-disabled" = "󰂲";
          "format-connected" = "";
          "tooltip-format" = "Devices connected: {num_connections}";
          "on-click" = "blueberry";
        };

        "network" = {
          "format-icons" = [
            "󰤯"
            "󰤟"
            "󰤢"
            "󰤥"
            "󰤨"
          ];
          "format" = "{icon}";
          "format-wifi" = "{icon}";
          "format-ethernet" = "󰀂";
          "format-disconnected" = "󰤮";
          "tooltip-format-wifi" = "{essid} ({frequency} GHz)\n⇣{bandwidthDownBytes}  ⇡{bandwidthUpBytes}";
          "tooltip-format-ethernet" = "⇣{bandwidthDownBytes}  ⇡{bandwidthUpBytes}";
          "tooltip-format-disconnected" = "Disconnected";
          "interval" = 3;
          "spacing" = 1;
          "on-click" = "nixdots-launch-wifi";
        };

        "pulseaudio" = {
          "format" = "{icon}";
          "on-click" = "$TERMINAL --class=Wiremix -e wiremix";
          "on-click-right" = "pamixer -t";
          "tooltip-format" = "Playing at {volume}%";
          "scroll-step" = 5;
          "format-muted" = "";
          "format-icons" = {
            "default" = [
              ""
              ""
              ""
            ];
          };
        };

        "cpu" = {
          "interval" = 5;
          "format" = "󰍛";
          "on-click" = "$TERMINAL -e btop";
        };
      };
    };

    style = ''
      @define-color foreground #8a8a8d;
      @define-color background #1e1e1e;

      * {
        background-color: @background;
        color: @foreground;

        border: none;
        border-radius: 0;
        min-height: 0;
        font-family: Iosevka Nerd Font Mono;
        font-size: 12px;
      }

      .modules-left {
        margin-left: 8px;
      }

      .modules-right {
        margin-right: 8px;
      }

      #workspaces button {
        all: initial;
        padding: 0 6px;
        margin: 0 1.5px;
        min-width: 9px;
      }

      #workspaces button.empty {
        opacity: 0.5;
      }

      #tray,
      #cpu,
      #network,
      #bluetooth,
      #pulseaudio,
      #custom-nixdots,
      #custom-screenrecording-indicator{
        min-width: 12px;
        margin: 0 7.5px;
      }

      #custom-expand-icon {
        margin-right: 7px;
      }

      tooltip {
        padding: 2px;
      }

      #clock {
        margin-left: 8.75px;
      }

      .hidden {
        opacity: 0;
      }

      #custom-screenrecording-indicator {
        min-width: 12px;
        margin-left: 8.75px;
        font-size: 10px;
      }

      #custom-screenrecording-indicator.active {
        color: #a55555;
      }
    '';
  };
}
</file>

<file path="home/common/optional/desktop/default.nix">
{
  imports = [
    ./hyprland
    ./gtk-qt.nix
    ./walker.nix
  ];
}
</file>

<file path="home/common/optional/desktop/gtk-qt.nix">
{
  pkgs,
  lib,
  ...
}:
let
  catppuccinAccent = "Blue";
  catppuccinFlavor = "Mocha";

  catppuccinKvantum = pkgs.catppuccin-kvantum.override {
    accent = "${lib.toLower catppuccinAccent}";
    variant = "${lib.toLower catppuccinFlavor}";
  };
  qtThemeName = "catppuccin-${lib.toLower catppuccinFlavor}-${lib.toLower catppuccinAccent}";
in
{
  home.packages = with pkgs; [
    papirus-folders
    catppuccinKvantum
  ];

  gtk = {
    enable = true;
    theme = {
      name = "catppuccin-${lib.toLower catppuccinFlavor}-${lib.toLower catppuccinAccent}-standard";
      package = pkgs.catppuccin-gtk.override {
        accents = [ "${lib.toLower catppuccinAccent}" ];
        size = "standard";
        variant = "${lib.toLower catppuccinFlavor}";
      };
    };
    iconTheme = {
      name = "Papirus";
      package = pkgs.catppuccin-papirus-folders.override {
        flavor = "${lib.toLower catppuccinFlavor}";
        accent = "${lib.toLower catppuccinAccent}";
      };
    };

    gtk3.extraConfig.gtk-application-prefer-dark-theme = true;
  };

  qt = {
    enable = true;
    platformTheme.name = "kvantum";
    style.name = "kvantum";
  };

  xdg.configFile = {
    "Kvantum/${qtThemeName}".source = "${catppuccinKvantum}/share/Kvantum/${qtThemeName}";
    "Kvantum/kvantum.kvconfig".source = (pkgs.formats.ini { }).generate "kvantum.kvconfig" {
      General.theme = qtThemeName;
    };
  };
}
</file>

<file path="home/common/optional/desktop/walker.nix">
{
  inputs,
  ...
}:
{
  imports = [
    inputs.walker.homeManagerModules.default
  ];

  programs.walker = {
    enable = true;
    runAsService = true;
  };
}
</file>

<file path="home/common/optional/development/default.nix">
{ pkgs, ... }:
{
  home.packages = with pkgs; [
    arduino-ide
    godot
  ];
}
</file>

<file path="home/common/optional/media/spotify/default.nix">
{
  pkgs,
  config,
  lib,
  inputs,
  ...
}:
let
  spicePkgs = inputs.spicetify-nix.legacyPackages.${pkgs.system};
in
{
  imports = [ inputs.spicetify-nix.homeManagerModules.default ];
  # nixpkgs.config.allowUnfreePredicate = pkg: builtins.elem (lib.getName pkg) [ "spotify" ];
  programs.spicetify = {
    enable = true;

    theme = spicePkgs.themes.text;

    # colorScheme = "custom";
    # customColorScheme = {
    #   accent = config.hostSpec.theme.colors.base0B;
    #   accent-active = config.hostSpec.theme.colors.base0B;
    #   accent-inactive = config.hostSpec.theme.colors.base00;
    #   banner = config.hostSpec.theme.colors.base0B;
    #   border-active = config.hostSpec.theme.colors.base0B;
    #   border-inactive = config.hostSpec.theme.colors.base02;
    #   header = config.hostSpec.theme.colors.base04;
    #   highlight = config.hostSpec.theme.colors.base04;
    #   main = config.hostSpec.theme.colors.base00;
    #   notification = config.hostSpec.theme.colors.base0D;
    #   notification-error = config.hostSpec.theme.colors.base08;
    #   subtext = "a6adc8";
    #   text = config.hostSpec.theme.colors.base05;
    # };

    enabledExtensions = with spicePkgs.extensions; [
      autoSkipVideo
      fullAppDisplay
      playlistIcons
      fullAlbumDate
      skipStats
      songStats
      autoVolume
      history
      betterGenres
      hidePodcasts
      adblock
    ];
  };
}
</file>

<file path="home/common/optional/media/default.nix">
{ pkgs, ... }:
{
  imports = [
    ./spotify
  ];

  home.packages = builtins.attrValues {
    inherit (pkgs)
      ;
  };
}
</file>

<file path="home/common/optional/tools/default.nix">
{ pkgs, ... }:
{
  home.packages = builtins.attrValues {
    inherit (pkgs)
      # Development
      vscodium-fhs
      sqlitebrowser
      vscode
      insomnia
      rustup
      # Device Imaging
      # Productivity
      qbittorrent
      copyq
      drawio
      libreoffice
      obsidian
      # Security
      bitwarden
      # Media
      freetube
      # VM
      remmina
      # Other
      android-file-transfer
      localsend
      rpi-imager
      nvd
      # orca-slicer
      orca-slicer
      freecad-wayland
      ;
  };
}
</file>

<file path="hosts/common/core/default.nix">
{
  inputs,
  outputs,
  config,
  lib,
  pkgs,
  ...
}:
let
  platform = "nixos";
  platformModules = "${platform}Modules";
in
{
  imports = lib.flatten [
    inputs.home-manager.${platformModules}.home-manager
    inputs.nix-index-database.${platformModules}.nix-index
    { programs.nix-index-database.comma.enable = true; }

    (map lib.custom.relativeToRoot [
      "modules/common"
      "modules/hosts/common"
      "modules/hosts/${platform}"

      "hosts/common/core/${platform}.nix"

      "hosts/common/users/"
    ])
  ];

  hostSpec = {
    primaryUsername = "antoniosarro";
    username = "antoniosarro";
    handle = "antoniosarro";
    email = {
      github = "tech@antoniosarro.dev";
    };
  };

  networking.hostName = config.hostSpec.hostName;

  # System-wide packages, in case we log in as root
  environment.systemPackages = [ pkgs.openssh ];

  # Force home-manager to use global packages
  home-manager.useGlobalPkgs = true;
  # If there is a conflict file that is backed up, use this extension
  home-manager.backupFileExtension = "bk";

  # ===============================
  # Overlays
  # ===============================
  nixpkgs = {
    overlays = [
      outputs.overlays.default
    ];
    config = {
      allowUnfree = true;
    };
  };

  # ===============================
  # Basic Shell Enablement
  # ===============================
  programs.zsh = {
    enable = true;
    enableCompletion = true;
  };
}
</file>

<file path="hosts/common/core/nixos.nix">
{ config, lib, ... }:
{
  # Database for aiding terminal-based programs
  environment.enableAllTerminfo = true;
  # Enable firmware with a license allowing redistribution
  hardware.enableRedistributableFirmware = true;

  # This should be handled by config.security.pam.sshAgentAuth.enable
  security.sudo.extraConfig = ''
    Defaults lecture = never # rollback results in sudo lectures after each reboot, it's somewhat useless anyway
    Defaults pwfeedback # password input feedback - makes typed password visible as asterisks
    Defaults timestamp_timeout=120 # only ask for password every 2h
    # Keep SSH_AUTH_SOCK so that pam_ssh_agent_auth.so can do its magic.
    Defaults env_keep+=SSH_AUTH_SOCK
  '';

  # ============================
  # Nix Helper
  # ============================
  programs.nh = {
    enable = true;
    clean.enable = true;
    clean.extraArgs = "--keep-since 20d --keep 20";
    flake = "${config.hostSpec.home}/thinkdots";
  };

  # ============================
  # Localization
  # ============================
  i18n.defaultLocale = lib.mkDefault "en_US.UTF-8";
  time.timeZone = lib.mkDefault "Europe/Rome";
}
</file>

<file path="hosts/common/disks/laptop.nix">
{
  disko.devices = {
    disk = {
      main = {
        type = "disk";
        device = "/dev/nvme0n1";
        content = {
          type = "gpt";
          partitions = {
            ESP = {
              size = "512M";
              type = "EF00";
              content = {
                type = "filesystem";
                format = "vfat";
                mountpoint = "/boot";
                mountOptions = [ "umask=0077" ];
              };
            };
            luks = {
              size = "100%";
              content = {
                type = "luks";
                name = "crypted";
                passwordFile = "/tmp/secret.key";
                settings = {
                  allowDiscards = true;
                  bypassWorkqueues = true;
                };
                content = {
                  type = "btrfs";
                  extraArgs = [ "-f" ];
                  subvolumes = {
                    "/root" = {
                      mountpoint = "/";
                      mountOptions = [
                        "compress=zstd"
                        "noatime"
                      ];
                    };
                    "/home" = {
                      mountpoint = "/home";
                      mountOptions = [
                        "compress=zstd"
                        "noatime"
                      ];
                    };
                    "/nix" = {
                      mountpoint = "/nix";
                      mountOptions = [
                        "compress=zstd"
                        "noatime"
                      ];
                    };
                    "/swap" = {
                      mountpoint = "/.swapvol";
                      swap.swapfile.size = "16G";
                    };
                  };
                };
              };
            };
          };
        };
      };
    };
  };
}
</file>

<file path="hosts/common/optional/services/bluetooth.nix">
{ ... }:
{

  hardware.bluetooth = {
    enable = true;
    powerOnBoot = true;
  };

  services.blueman.enable = true;
}
</file>

<file path="hosts/common/optional/services/greetd.nix">
{
  config,
  pkgs,
  lib,
  ...
}:
{
  config = {
    services.greetd = {
      enable = true;
      restart = true;

      settings = {
        default_session = {
          command = "${pkgs.greetd.tuigreet}/bin/tuigreet --asterisks --time --time-format '%I:%M %p | %a • %h | %F' --cmd Hyprland";
          # command = "${pkgs.greetd.tuigreet}/bin/tuigreet --asterisks --time --time-format '%I:%M %p | %a • %h | %F' --cmd uwsm start -- hyprland-uwsm.desktop";
          user = "antoniosarro";
        };
      };
    };
  };
}
</file>

<file path="hosts/common/optional/services/logrotate.nix">
{ config, ... }:
{
  services.logrotate = {
    enable = true;
    settings = {
      "/run/user/*/hypr/*/hyprland.log" = {
        size = "1000M";
        rotate = 5;
        compress = true;
        nocompress = true;
        missingok = true;
        notifempty = true;
        dateext = true;
      };
    };
  };
  assertions = [
    {
      assertion = (config.programs.hyprland.enable == true);
      message = "hyprland.log rotation expects that hyprland is enabled.";
    }
  ];

}
</file>

<file path="hosts/common/optional/services/printing.nix">
{ pkgs, ... }:
{
  services.printing = {
    enable = true;
    drivers = [ pkgs.epson-escpr ];
  };

  systemd.services.cups-browsed = {
    enable = false;
    unitConfig.Mask = true;
  };

  hardware.printers = {
    ensurePrinters = [
      {
        name = "Epson_XP_305";
        location = "Home";
        deviceUri = "lpd://10.10.40.16:515/PASSTHRU";
        model = "epson-inkjet-printer-escpr/Epson-XP-302_303_305_306_Series-epson-escpr-en.ppd";
        ppdOptions = {
          PageSize = "A4";
        };
      }
    ];
  };
}
</file>

<file path="hosts/common/optional/audio.nix">
{ pkgs, ... }:
{
  services.pulseaudio.enable = false;
  security.rtkit.enable = true;

  services.pipewire = {
    enable = true;
    package = pkgs.unstable.pipewire;
    alsa = {
      enable = true;
      support32Bit = true;
    };
    pulse.enable = true;
    jack.enable = true;
    wireplumber.enable = true;
  };

  environment.systemPackages = builtins.attrValues {
    inherit (pkgs)
      easyeffects
      pwvucontrol
      ;
  };
}
</file>

<file path="hosts/common/optional/flatpak.nix">
{
  pkgs,
  ...
}:
let
  grep = pkgs.gnugrep;
  desiredFlatpaks = [
    "com.github.tchx84.Flatseal"
    "io.github.flattool.Warehouse"
    "org.raspberrypi.rpi-imager"
  ];
in
{
  services.flatpak.enable = true;
  system.userActivationScripts.flatpakManagement = {
    text = ''
      # Ensure the Flathub repo is added
      ${pkgs.flatpak}/bin/flatpak remote-add --if-not-exists flathub \
        https://flathub.org/repo/flathub.flatpakrepo

      # Get currently installed Flatpaks
      installedFlatpaks=$(${pkgs.flatpak}/bin/flatpak list --app --columns=application)

      # Remove any Flatpaks that are NOT in the desired list
      for installed in $installedFlatpaks; do
        if ! echo ${toString desiredFlatpaks} | ${grep}/bin/grep -q $installed; then
          echo "Removing $installed because it's not in the desiredFlatpaks list."
          ${pkgs.flatpak}/bin/flatpak uninstall -y --noninteractive $installed
        fi
      done

      # Install or re-install the Flatpaks you DO want
      for app in ${toString desiredFlatpaks}; do
        echo "Ensuring $app is installed."
        ${pkgs.flatpak}/bin/flatpak install -y flathub $app
      done

      # 6. Remove unused Flatpaks
      ${pkgs.flatpak}/bin/flatpak uninstall --unused -y

      # 7. Update all installed Flatpaks
      ${pkgs.flatpak}/bin/flatpak update -y
    '';
  };
}
</file>

<file path="hosts/common/optional/fonts.nix">
{
  pkgs,
  ...
}:
{
  fonts = {
    enableDefaultPackages = true;
    fontDir.enable = true;
    packages = (
      builtins.attrValues {
        inherit (pkgs)
          eb-garamond
          overpass
          noto-fonts
          noto-fonts-emoji
          ;

        inherit (pkgs.unstable.nerd-fonts)
          iosevka
          symbols-only
          ;
      }
    );
    fontconfig.defaultFonts = {
      serif = [
        "Noto Color Emoji"
        "EB Garamond"
        "Nerd Fonts Symbols Only"
      ];
      sansSerif = [
        "Noto Color Emoji"
        "Nerd Fonts Symbols Only"
        "Overpass"
      ];
      monospace = [
        "Noto Color Emoji"
        "Iosevka Nerd Font Mono"
        "Nerd Fonts Symbols Only"
      ];
      emoji = [
        "Noto Color Emoji"
        "Nerd Fonts Symbols Only"
      ];
    };
  };
}
</file>

<file path="hosts/common/optional/gpu.nix">
{
  pkgs,
  ...
}:
{
  hardware = {
    graphics = {
      enable = true;
      enable32Bit = true;
      package = pkgs.unstable.mesa;
      package32 = pkgs.unstable.pkgsi686Linux.mesa;
    };
  };
}
</file>

<file path="hosts/common/optional/graphene.nix">
{ config, ... }:
{
  programs.adb.enable = true;
  users.users.${config.hostSpec.username}.extraGroups = [
    "adbusers"
    "kvm"
  ];
}
</file>

<file path="hosts/common/optional/hyprland.nix">
{
  inputs,
  pkgs,
  config,
  lib,
  ...
}:
{
  # environment.loginShellInit = ''
  #   if uwsm check may-start && uwsm select; then
  #     exec uwsm start default
  #   fi
  # '';

  programs.hyprland = {
    enable = true;
    package = pkgs.unstable.hyprland;
  };

  environment.systemPackages = [
    inputs.rose-pine-hyprcursor.packages.${pkgs.system}.default
  ];

  assertions = [
    {
      assertion =
        config.programs.hyprland.package
        == config.home-manager.users.${config.hostSpec.primaryUsername}.wayland.windowManager.hyprland.package;
      message = "NixOS and Home-manager Hyprland package must be the same version.";
    }
    {
      assertion = (
        lib.strings.compareVersions config.hardware.graphics.package.version "25.2.0" == -1
        || lib.strings.compareVersions config.programs.hyprland.package.version "0.51.0" != -1
      );
      message = "Mesa 25.2.x has breaking ABI change, Hyprland 0.51.0 or newer is required.";
    }
  ];
}
</file>

<file path="hosts/common/optional/mpv.nix">
{ pkgs, ... }:
{
  environment.systemPackages = [ pkgs.mpv ];
}
</file>

<file path="hosts/common/optional/obsidian.nix">
{ pkgs, ... }:
{
  environment.systemPackages = [ pkgs.obsidian ];
}
</file>

<file path="hosts/common/optional/plymouth.nix">
{
  lib,
  pkgs,
  ...
}:
{
  environment.systemPackages = [ pkgs.adi1090x-plymouth-themes ];
  boot = {
    kernelParams = [
      "quiet"
    ];
    plymouth = {
      enable = true;
      theme = lib.mkForce "deus_ex";
      themePackages = [
        (pkgs.adi1090x-plymouth-themes.override { selected_themes = [ "deus_ex" ]; })
      ];
    };
    consoleLogLevel = 0;
  };
}
</file>

<file path="hosts/common/optional/protonvpn.nix">
{ pkgs, ... }:
{
  environment.systemPackages = [
    pkgs.protonvpn-cli
    pkgs.protonvpn-gui
  ];

  networking.firewall.checkReversePath = false;
}
</file>

<file path="hosts/common/optional/thunar.nix">
{ pkgs, ... }:
{
  programs = {
    thunar = {
      enable = true;
      plugins = builtins.attrValues {
        inherit (pkgs.xfce)
          thunar-archive-plugin
          thunar-media-tags-plugin
          thunar-volman
          ;
      };
    };
    xfconf.enable = true; # persist Thunar settings
    file-roller.enable = true; # required for Thunar archive plugin
  };

  services = {
    gvfs.enable = true; # for stuff like Trash folders etc
    udisks2.enable = true; # storage device manipulation
    tumbler.enable = true; # thumbnail generation service for Thunar
  };
}
</file>

<file path="hosts/common/optional/virtualisation.nix">
{
  pkgs,
  config,
  inputs,
  ...
}:
let
  virtLib = inputs.nixvirt.lib;
in
{
  imports = [
    inputs.nixvirt.nixosModules.default
  ];

  boot.kernelModules = [ "vfio-pci" ];

  virtualisation = {
    docker = {
      enable = true;
      autoPrune = {
        enable = true;
        dates = "weekly";
      };
      storageDriver = "overlay2";
      liveRestore = true;
      daemon.settings = {
        default-address-pools = [
          {
            base = "172.17.0.0/12";
            size = 20;
          }
        ];
        ip = "127.0.0.1";
        experimental = true;
      };
    };
    oci-containers = {
      backend = "docker";
      containers = {
        "portainer" = {
          autoStart = true;
          image = "portainer/portainer-ce:lts";
          volumes = [
            "/var/run/docker.sock:/var/run/docker.sock"
            "portainer_data:/data"
          ];
          ports = [
            "8000:8000"
            "9443:9443"
          ];
        };
      };
    };
    libvirtd = {
      enable = true;
      qemu = {
        package = pkgs.qemu_kvm;
        runAsRoot = true;
        ovmf = {
          enable = true;
        };
      };
    };
    libvirt = {
      enable = true;
      connections = {
        "qemu:///system" = {
          networks = [
            {
              active = true;
              definition = virtLib.network.writeXML {
                uuid = "8e91d351-e902-4fce-99b6-e5ea88ac9b80";
                name = "vm-lan";
                forward = {
                  mode = "nat";
                  nat = {
                    nat = {
                      port = {
                        start = 1024;
                        end = 65535;
                      };
                    };
                    ipv6 = false;
                  };
                };
                bridge = {
                  name = "virbr0";
                  stp = true;
                  delay = 0;
                };
                ipv6 = false;
                ip = {
                  address = "192.168.1.0";
                  netmask = "255.255.255.0";
                  dhcp = {
                    range = {
                      start = "192.168.1.100";
                      end = "192.168.1.254";
                    };
                  };
                };
              };
            }
          ];
        };
      };
    };
  };

  programs.virt-manager.enable = true;

  environment.systemPackages = [
    pkgs.qemu_kvm
    pkgs.qemu
  ];

  users.users.${config.hostSpec.username} = {
    extraGroups = [ "libvirtd" ];
  };
}
</file>

<file path="hosts/common/optional/wifi.nix">
{ pkgs, ... }:
{
  networking.networkmanager.dispatcherScripts = [
    {
      type = "basic";

      source =
        pkgs.writeText "disable-wireless-when-wired" # sh
          ''
            IFACE=$1
            ACTION=$2
            nmcli=${pkgs.networkmanager}/bin/nmcli

            case ''${IFACE} in
                eth*|en*)
                    case ''${ACTION} in
                        up)
                            logger "disabling wifi radio"
                            $nmcli radio wifi off
                            ;;
                        down)
                            logger "enabling wifi radio"
                            $nmcli radio wifi on
                            ;;
                    esac
                    ;;
            esac
          '';
    }
  ];
}
</file>

<file path="hosts/common/users/antoniosarro/nixos.nix">
{
  config,
  lib,
  ...
}:
{
  isNormalUser = true;
  extraGroups =
    let
      ifTheyExist = groups: builtins.filter (group: builtins.hasAttr group config.users.groups) groups;
    in
    lib.flatten [
      "wheel"
      (ifTheyExist [
        "audio"
        "video"
        "docker"
        "git"
        "networkmanager"
        "scanner"
        "lp"
      ])
    ];
}
</file>

<file path="hosts/common/users/default.nix">
{
  inputs,
  pkgs,
  config,
  lib,
  ...
}:
let
  platform = "nixos";
  hostSpec = config.hostSpec;

  pubKeys = lib.filesystem.listFilesRecursive (
    lib.custom.relativeToRoot "hosts/common/users/${hostSpec.primaryUsername}/keys/"
  );

  primaryUserPubKeys = lib.lists.forEach pubKeys (key: builtins.readFile key);
in
{
  programs.zsh.enable = true;
  programs.git.enable = true;
  environment = {
    systemPackages = [
      pkgs.just
    ];
  };

  users = {
    users =
      (lib.mergeAttrsList (
        map (user: {
          "${user}" =
            let
              platformPath = lib.custom.relativeToRoot "hosts/common/users/${user}/${platform}.nix";
            in
            {
              name = user;
              shell = pkgs.zsh;
              openssh.authorizedKeys.keys = primaryUserPubKeys;
              home = "/home/${user}";
              hashedPassword = "$y$j9T$fWn4.HzmsT7aryHo34PfC.$wlH3KX5.fdTztxgS8kYNCOTqD1RcebdP0S6q1Z4kT77";
            }
            // lib.optionalAttrs (lib.pathExists platformPath) (
              import platformPath {
                inherit config lib;
              }
            );
        }) config.hostSpec.users
      ))
      // {
        root = {
          shell = pkgs.zsh;
          # hashedPasswordFile = config.users.users.${config.hostSpec.primaryUsername}.hashedPasswordFile;
          hashedPassword = "$y$j9T$fWn4.HzmsT7aryHo34PfC.$wlH3KX5.fdTztxgS8kYNCOTqD1RcebdP0S6q1Z4kT77";
          # root's ssh key are mainly used for remote deployment
          openssh.authorizedKeys.keys =
            config.users.users.${config.hostSpec.primaryUsername}.openssh.authorizedKeys.keys;
        };
      };
  }
  // {
    mutableUsers = false;
  };
}
// lib.optionalAttrs (inputs ? "home-manager") {
  home-manager =
    let
      fullPathIfExists =
        path:
        let
          fullPath = lib.custom.relativeToRoot path;
        in
        lib.optional (lib.pathExists fullPath) fullPath;
    in
    {
      extraSpecialArgs = {
        inherit pkgs inputs;
        hostSpec = config.hostSpec;
      };

      users =
        (lib.mergeAttrsList (
          map (user: {
            "${user}".imports = lib.flatten [
              (lib.optional (!hostSpec.isMinimal) (
                map (fullPathIfExists) [
                  "home/${user}/${hostSpec.hostName}.nix"
                  "home/${user}/common"
                  "home/${user}/common/${platform}.nix"
                ]
              ))
              (
                { ... }:
                {
                  home = {
                    homeDirectory = "/home/${user}";
                    username = "${user}";
                  };
                }
              )
            ];
          }) config.hostSpec.users
        ))
        // {
          root = {
            home.stateVersion = "25.05";
            programs.zsh = {
              enable = true;
            };
          };
        };
    };
}
</file>

<file path="hosts/nixos/laptop/default.nix">
# ===================================================================
#
# Main Laptop
# NixOS Running on Intel I7
#
# ===================================================================

{
  inputs,
  lib,
  config,
  pkgs,
  ...
}:
{
  imports = lib.flatten [
    # ============================
    # Hardware
    # ============================
    ./hardware-configuration.nix
    inputs.hardware.nixosModules.lenovo-thinkpad-t14
    inputs.hardware.nixosModules.common-cpu-intel
    inputs.hardware.nixosModules.common-gpu-intel

    # ============================
    # Disk Layout
    # ============================
    inputs.disko.nixosModules.disko
    (lib.custom.relativeToRoot "hosts/common/disks/laptop.nix")

    (map lib.custom.relativeToRoot [
      # Required Configs
      "hosts/common/core"

      # Services
      "hosts/common/optional/services/bluetooth.nix"
      "hosts/common/optional/services/greetd.nix"
      "hosts/common/optional/services/logrotate.nix"
      "hosts/common/optional/services/printing.nix"

      # Optional Configs
      "hosts/common/optional/audio.nix"
      "hosts/common/optional/flatpak.nix"
      "hosts/common/optional/fonts.nix"
      "hosts/common/optional/gpu.nix"
      "hosts/common/optional/graphene.nix"
      "hosts/common/optional/hyprland.nix"
      "hosts/common/optional/mpv.nix"
      "hosts/common/optional/obsidian.nix"
      "hosts/common/optional/protonvpn.nix"
      "hosts/common/optional/thunar.nix"
      "hosts/common/optional/virtualisation.nix"
      "hosts/common/optional/wifi.nix"
    ])
  ];

  # ============================
  # Host Specification
  # ============================
  hostSpec = {
    hostName = "laptop";
    username = "antoniosarro";
    users = [ "antoniosarro" ];
  };

  networking = {
    networkmanager.enable = true;
    enableIPv6 = false;
  };

  boot.loader = {
    systemd-boot = {
      enable = true;
      configurationLimit = lib.mkDefault 10;
    };
    efi.canTouchEfiVariables = true;
    timeout = 3;
  };

  boot.initrd = {
    systemd.enable = true;
  };

  boot.kernelPackages = pkgs.linuxPackages_latest;

  system.stateVersion = "25.05";
}
</file>

<file path="hosts/nixos/laptop/hardware-configuration.nix">
# Do not modify this file!  It was generated by ‘nixos-generate-config’
# and may be overwritten by future invocations.  Please make changes
# to /etc/nixos/configuration.nix instead.
{
  config,
  lib,
  pkgs,
  modulesPath,
  ...
}:
{
  imports = [
    (modulesPath + "/installer/scan/not-detected.nix")
  ];

  boot.initrd.availableKernelModules = [
    "xhci_pci"
    "thunderbolt"
    "nvme"
    "usb_storage"
    "sd_mod"
    "sdhci_pci"
  ];
  boot.initrd.kernelModules = [ ];
  boot.kernelModules = [ "kvm-intel" ];
  boot.extraModulePackages = [ ];

  # Enables DHCP on each ethernet and wireless interface. In case of scripted networking
  # (the default) this is the recommended approach. When using systemd-networkd it's
  # still possible to use this option, but it's recommended to use it in conjunction
  # with explicit per-interface declarations with `networking.interfaces.<interface>.useDHCP`.
  networking.useDHCP = lib.mkDefault true;
  # networking.interfaces.enp2s0.useDHCP = lib.mkDefault true;
  # networking.interfaces.wlp3s0.useDHCP = lib.mkDefault true;

  nixpkgs.hostPlatform = lib.mkDefault "x86_64-linux";
  hardware.cpu.intel.updateMicrocode = lib.mkDefault config.hardware.enableRedistributableFirmware;
}
</file>

<file path="lib/default.nix">
{ lib, ... }:
{
  # use path relative to the root of the project
  relativeToRoot = lib.path.append ../.;
  scanPaths =
    path:
    builtins.map (f: (path + "/${f}")) (
      builtins.attrNames (
        lib.attrsets.filterAttrs (
          path: _type:
          (_type == "directory") # include directories
          || (
            (path != "default.nix") # ignore default.nix
            && (lib.strings.hasSuffix ".nix" path) # include .nix files
          )
        ) (builtins.readDir path)
      )
    );
}
</file>

<file path="modules/common/default.nix">
{ lib, ... }:
{
  imports = lib.custom.scanPaths ./.;
}
</file>

<file path="modules/common/host-spec.nix">
{
  config,
  pkgs,
  lib,
  ...
}:
{
  options.hostSpec = lib.mkOption {
    type = lib.types.submodule {
      freeformType = with lib.types; attrsOf str;

      options = {
        primaryUsername = lib.mkOption {
          type = lib.types.str;
          description = "The primary username of the host";
        };
        hostName = lib.mkOption {
          type = lib.types.str;
          description = "The hostname of the host";
        };
        email = lib.mkOption {
          type = lib.types.attrsOf lib.types.str;
          description = "The email of the user";
        };
        networking = lib.mkOption {
          default = { };
          type = lib.types.attrsOf lib.types.anything;
          description = "An attribute set of networking information";
        };
        wifi = lib.mkOption {
          type = lib.types.bool;
          default = false;
          description = "Used to indicate if a host has wifi";
        };
        domain = lib.mkOption {
          type = lib.types.str;
          default = "local"; # Need a default for the installer
          description = "The domain of the host";
        };
        userFullName = lib.mkOption {
          type = lib.types.str;
          description = "The full name of the user";
        };
        handle = lib.mkOption {
          type = lib.types.str;
          description = "The handle of the user (eg: github user)";
        };
        home = lib.mkOption {
          type = lib.types.str;
          description = "The home directory of the user";
          default =
            let
              user = config.hostSpec.primaryUsername;
            in
            if pkgs.stdenv.isLinux then "/home/${user}" else "/Users/${user}";
        };
        users = lib.mkOption {
          type = lib.types.listOf lib.types.str;
          description = "An attribute set of all users on the host";
          default = [ config.hostSpec.username ];
        };
        isMinimal = lib.mkOption {
          type = lib.types.bool;
          default = false;
          description = "Indicate a minimal host";
        };
        isProduction = lib.mkOption {
          type = lib.types.bool;
          default = true;
          description = "Indicate a production host";
        };
        isServer = lib.mkOption {
          type = lib.types.bool;
          default = false;
          description = "Indicate a server host";
        };
        isWork = lib.mkOption {
          type = lib.types.bool;
          default = false;
          description = "Indicate a host that uses work resources";
        };
        isDevelopment = lib.mkOption {
          type = lib.types.bool;
          default = false;
          description = "Indicate a host used for development";
        };
        isMobile = lib.mkOption {
          type = lib.types.bool;
          default = false;
          description = "Used to indicate a mobile host";
        };
        useYubikey = lib.mkOption {
          type = lib.types.bool;
          default = false;
          description = "Indicate if the host uses a yubikey";
        };
        useWayland = lib.mkOption {
          type = lib.types.bool;
          default = false;
          description = "Indicate a host that uses Wayland";
        };
        defaultBrowser = lib.mkOption {
          type = lib.types.str;
          default = "firefox";
          description = "The default browser to use on the host";
        };
        defaultEditor = lib.mkOption {
          type = lib.types.str;
          default = "nvim";
          description = "The default editor command to use on the host";
        };
        defaultDesktop = lib.mkOption {
          type = lib.types.str;
          default = "Hyprland";
          description = "The default desktop environment to use on the host";
        };
      };
    };
  };

  config = {
    assertions =
      let
        # We import these options to HM and NixOS, so need to not fail on HM
        isImpermanent =
          config ? "system" && config.system ? "impermanence" && config.system.impermanence.enable;
      in
      [
        {
          assertion =
            !config.hostSpec.isWork || (config.hostSpec.isWork && !builtins.isNull config.hostSpec.work);
          message = "isWork is true but no work attribute set is provided";
        }
      ];
  };
}
</file>

<file path="modules/common/nix.nix">
{
  inputs,
  config,
  lib,
  pkgs,
  ...
}:
{
  nixpkgs.config = {
    allowBroken = true;
    allowUnfree = true;
  };

  nix = {
    package = lib.mkForce pkgs.unstable.nixVersions.latest;
    optimise = {
      automatic = true;
      dates = [ "22:00" ];
    };
    settings = {
      connect-timeout = 5;
      log-lines = 25;
      min-free = 128000000;
      max-free = 1000000000;
      experimental-features = lib.mkDefault "nix-command flakes";
      warn-dirty = false;
      allow-import-from-derivation = true;
      trusted-users = [ "@wheel" ];
      builders-use-substitutes = true;
      fallback = true;
      substituters = [
        "https://cache.nixos.org" # Official global cache
        "https://nix-community.cachix.org" # Community packages
      ];
    };
  }
  // (lib.optionalAttrs pkgs.stdenv.isLinux {
    registry = lib.mapAttrs (_: value: { flake = value; }) inputs;
    nixPath = lib.mapAttrsToList (key: value: "${key}=${value.to.path}") config.nix.registry;
  });
}
</file>

<file path="modules/home/default.nix">
{ lib, ... }:
{
  imports = lib.custom.scanPaths ./.;
}
</file>

<file path="modules/home/monitors.nix">
{ lib, config, ... }:
{
  options.monitors = lib.mkOption {
    type = lib.types.listOf (
      lib.types.submodule {
        options = {
          name = lib.mkOption {
            type = lib.types.str;
            example = "DP-1";
          };
          primary = lib.mkOption {
            type = lib.types.bool;
            default = false;
          };
          noBar = lib.mkOption {
            type = lib.types.bool;
            default = false;
          };
          width = lib.mkOption {
            type = lib.types.int;
            example = 1920;
          };
          height = lib.mkOption {
            type = lib.types.int;
            example = 1080;
          };
          refreshRate = lib.mkOption {
            type = lib.types.int;
            default = 60;
          };
          x = lib.mkOption {
            type = lib.types.int;
            default = 0;
          };
          y = lib.mkOption {
            type = lib.types.int;
            default = 0;
          };
          scale = lib.mkOption {
            type = lib.types.number;
            default = 1.0;
          };
          transform = lib.mkOption {
            type = lib.types.int;
            default = 0;
          };
          enabled = lib.mkOption {
            type = lib.types.bool;
            default = true;
          };
          workspace = lib.mkOption {
            type = lib.types.nullOr lib.types.str;
            description = "Defines a workspace that should persist on this monitor.";
            default = null;
          };
          vrr = lib.mkOption {
            type = lib.types.int;
            description = "Variable Refresh Rate aka Adaptive Sync aka AMD FreeSync.\nValues are oriented towards hyprland's vrr values which are:\n0 = off, 1 = on, 2 = fullscreen only\nhttps://wiki.hyprland.org/Configuring/Variables/#misc";
            default = 0;
          };
        };
      }
    );
    default = [ ];
  };
  config = {
    assertions = [
      {
        assertion =
          ((lib.length config.monitors) != 0)
          -> ((lib.length (lib.filter (m: m.primary) config.monitors)) == 1);
        message = "Exactly one monitor must be set to primary.";
      }
    ];
  };
}
</file>

<file path="modules/hosts/common/default.nix">
{ lib, ... }:
{
  imports = lib.custom.scanPaths ./.;
}
</file>

<file path="modules/hosts/nixos/default.nix">
{ lib, ... }:
{
  imports = lib.custom.scanPaths ./.;
}
</file>

<file path="modules/hosts/nixos/warnings.nix">
{ config, lib, ... }:
{
  options = with lib; {
    configOptions.silencedWarnings = mkOption {
      type = types.listOf types.str;
      default = [ ];
      description = ''
        list of `config.warnings` values you want to ignore, verbatim.
      '';
    };
    warnings = mkOption {
      apply = builtins.filter (w: !(builtins.elem w config.configOptions.silencedWarnings));
    };
  };
}
</file>

<file path="overlays/default.nix">
{ inputs, ... }:
let
  additions =
    final: prev:
    (prev.lib.packagesFromDirectoryRecursive {
      callPackage = prev.lib.callPackageWith final;
      directory = ../pkgs/common;
    });

  linuxModifications = final: prev: prev.lib.mkIf final.stdenv.isLinux { };

  modifications = final: prev: {
  };

  stable-packages = final: _prev: {
    stable = import inputs.nixpkgs-stable {
      inherit (final) system;
      config.allowUnfree = true;
    };
  };

  unstable-packages = final: _prev: {
    unstable = import inputs.nixpkgs-unstable {
      inherit (final) system;
      config.allowUnfree = true;
    };
  };

in
{
  default =
    final: prev:

    (additions final prev)
    // (modifications final prev)
    // (linuxModifications final prev)
    // (stable-packages final prev)
    // (unstable-packages final prev);
}
</file>

<file path="checks.nix">
{
  inputs,
  system,
  pkgs,
  ...
}:
{
  bats-test =
    pkgs.runCommand "bats-test"
      {
        src = ../.;
        buildInputs = builtins.attrValues { inherit (pkgs) bats yq-go inetutils; };
      }
      ''
        cd $src
        bats tests
        touch $out
      '';

  pre-commit-check = inputs.pre-commit-hooks.lib.${system}.run {
    src = ./.;
    default_stages = [ "pre-commit" ];
    hooks = {
      # ========== General ==========
      check-added-large-files = {
        enable = true;
        excludes = [
          "\\.png"
          "\\.jpg"
        ];
      };
      check-case-conflicts.enable = true;
      check-executables-have-shebangs.enable = true;
      check-shebang-scripts-are-executable.enable = false;
      check-merge-conflicts.enable = true;
      detect-private-keys.enable = true;
      fix-byte-order-marker.enable = true;
      mixed-line-endings.enable = true;
      trim-trailing-whitespace.enable = true;

      # ========== Git ==========
      forbid-submodules = {
        enable = true;
        name = "forbid submodules";
        description = "forbids any submodules in the repository";
        language = "fail";
        entry = "submodules are not allowed in this repository:";
        types = [ "directory" ];
      };

      destroyed-symlinks = {
        enable = true;
        name = "destroyed-symlinks";
        description = "detects symlinks which are changed to regular files with a content of a path which that symlink was pointing to.";
        package = inputs.pre-commit-hooks.checks.${system}.pre-commit-hooks;
        entry = "${inputs.pre-commit-hooks.checks.${system}.pre-commit-hooks}/bin/destroyed-symlinks";
        types = [ "symlink" ];
      };

      # ========== Formatting ==========
      nixfmt-rfc-style.enable = true;
      deadnix = {
        enable = true;
        settings = {
          noLambdaArg = true;
        };
      };

      # ========== ShellScripts ==========
      shfmt.enable = true;
      shellcheck.enable = true;

      end-of-file-fixer.enable = true;
    };
  };
}
</file>

<file path="flake.lock">
{
  "nodes": {
    "disko": {
      "inputs": {
        "nixpkgs": [
          "nixpkgs"
        ]
      },
      "locked": {
        "lastModified": 1761899396,
        "narHash": "sha256-XOpKBp6HLzzMCbzW50TEuXN35zN5WGQREC7n34DcNMM=",
        "owner": "nix-community",
        "repo": "disko",
        "rev": "6f4cf5abbe318e4cd1e879506f6eeafd83f7b998",
        "type": "github"
      },
      "original": {
        "owner": "nix-community",
        "repo": "disko",
        "type": "github"
      }
    },
    "elephant": {
      "inputs": {
        "nixpkgs": "nixpkgs",
        "systems": "systems"
      },
      "locked": {
        "lastModified": 1762020869,
        "narHash": "sha256-5NB1EthU0z2USsL7AmyB9h7VUQ9Ey6qDCwknrJBzc4U=",
        "owner": "abenz1267",
        "repo": "elephant",
        "rev": "21aced0d304735d46b86f06a7f91b0fed4e5792f",
        "type": "github"
      },
      "original": {
        "owner": "abenz1267",
        "repo": "elephant",
        "type": "github"
      }
    },
    "flake-compat": {
      "flake": false,
      "locked": {
        "lastModified": 1747046372,
        "narHash": "sha256-CIVLLkVgvHYbgI2UpXvIIBJ12HWgX+fjA8Xf8PUmqCY=",
        "owner": "edolstra",
        "repo": "flake-compat",
        "rev": "9100a0f413b0c601e0533d1d94ffd501ce2e7885",
        "type": "github"
      },
      "original": {
        "owner": "edolstra",
        "repo": "flake-compat",
        "type": "github"
      }
    },
    "gitignore": {
      "inputs": {
        "nixpkgs": [
          "pre-commit-hooks",
          "nixpkgs"
        ]
      },
      "locked": {
        "lastModified": 1709087332,
        "narHash": "sha256-HG2cCnktfHsKV0s4XW83gU3F57gaTljL9KNSuG6bnQs=",
        "owner": "hercules-ci",
        "repo": "gitignore.nix",
        "rev": "637db329424fd7e46cf4185293b9cc8c88c95394",
        "type": "github"
      },
      "original": {
        "owner": "hercules-ci",
        "repo": "gitignore.nix",
        "type": "github"
      }
    },
    "hardware": {
      "locked": {
        "lastModified": 1761933221,
        "narHash": "sha256-rNHeoG3ZrA94jczyLSjxCtu67YYPYIlXXr0uhG3wNxM=",
        "owner": "nixos",
        "repo": "nixos-hardware",
        "rev": "7467f155fcba189eb088a7601f44fbef7688669b",
        "type": "github"
      },
      "original": {
        "owner": "nixos",
        "repo": "nixos-hardware",
        "type": "github"
      }
    },
    "home-manager": {
      "inputs": {
        "nixpkgs": [
          "nixpkgs"
        ]
      },
      "locked": {
        "lastModified": 1758463745,
        "narHash": "sha256-uhzsV0Q0I9j2y/rfweWeGif5AWe0MGrgZ/3TjpDYdGA=",
        "owner": "nix-community",
        "repo": "home-manager",
        "rev": "3b955f5f0a942f9f60cdc9cacb7844335d0f21c3",
        "type": "github"
      },
      "original": {
        "owner": "nix-community",
        "ref": "release-25.05",
        "repo": "home-manager",
        "type": "github"
      }
    },
    "home-manager_2": {
      "inputs": {
        "nixpkgs": [
          "zen-browser",
          "nixpkgs"
        ]
      },
      "locked": {
        "lastModified": 1752603129,
        "narHash": "sha256-S+wmHhwNQ5Ru689L2Gu8n1OD6s9eU9n9mD827JNR+kw=",
        "owner": "nix-community",
        "repo": "home-manager",
        "rev": "e8c19a3cec2814c754f031ab3ae7316b64da085b",
        "type": "github"
      },
      "original": {
        "owner": "nix-community",
        "repo": "home-manager",
        "type": "github"
      }
    },
    "hyprlang": {
      "inputs": {
        "nixpkgs": [
          "rose-pine-hyprcursor",
          "nixpkgs"
        ],
        "systems": "systems_2"
      },
      "locked": {
        "lastModified": 1709914708,
        "narHash": "sha256-bR4o3mynoTa1Wi4ZTjbnsZ6iqVcPGriXp56bZh5UFTk=",
        "owner": "hyprwm",
        "repo": "hyprlang",
        "rev": "a685493fdbeec01ca8ccdf1f3655c044a8ce2fe2",
        "type": "github"
      },
      "original": {
        "owner": "hyprwm",
        "repo": "hyprlang",
        "type": "github"
      }
    },
    "nix-index-database": {
      "inputs": {
        "nixpkgs": [
          "nixpkgs"
        ]
      },
      "locked": {
        "lastModified": 1762055842,
        "narHash": "sha256-Pu1v3mlFhRzZiSxVHb2/i/f5yeYyRNqr0RvEUJ4UgHo=",
        "owner": "nix-community",
        "repo": "nix-index-database",
        "rev": "359ff6333a7b0b60819d4c20ed05a3a1f726771f",
        "type": "github"
      },
      "original": {
        "owner": "nix-community",
        "repo": "nix-index-database",
        "type": "github"
      }
    },
    "nixpkgs": {
      "locked": {
        "lastModified": 1760284886,
        "narHash": "sha256-TK9Kr0BYBQ/1P5kAsnNQhmWWKgmZXwUQr4ZMjCzWf2c=",
        "owner": "NixOS",
        "repo": "nixpkgs",
        "rev": "cf3f5c4def3c7b5f1fc012b3d839575dbe552d43",
        "type": "github"
      },
      "original": {
        "owner": "NixOS",
        "ref": "nixos-unstable",
        "repo": "nixpkgs",
        "type": "github"
      }
    },
    "nixpkgs-stable": {
      "locked": {
        "lastModified": 1761597516,
        "narHash": "sha256-wxX7u6D2rpkJLWkZ2E932SIvDJW8+ON/0Yy8+a5vsDU=",
        "owner": "NixOS",
        "repo": "nixpkgs",
        "rev": "daf6dc47aa4b44791372d6139ab7b25269184d55",
        "type": "github"
      },
      "original": {
        "owner": "NixOS",
        "ref": "nixos-25.05",
        "repo": "nixpkgs",
        "type": "github"
      }
    },
    "nixpkgs-unstable": {
      "locked": {
        "lastModified": 1761907660,
        "narHash": "sha256-kJ8lIZsiPOmbkJypG+B5sReDXSD1KGu2VEPNqhRa/ew=",
        "owner": "NixOS",
        "repo": "nixpkgs",
        "rev": "2fb006b87f04c4d3bdf08cfdbc7fab9c13d94a15",
        "type": "github"
      },
      "original": {
        "owner": "NixOS",
        "ref": "nixos-unstable",
        "repo": "nixpkgs",
        "type": "github"
      }
    },
    "nixpkgs_2": {
      "locked": {
        "lastModified": 1761597516,
        "narHash": "sha256-wxX7u6D2rpkJLWkZ2E932SIvDJW8+ON/0Yy8+a5vsDU=",
        "owner": "NixOS",
        "repo": "nixpkgs",
        "rev": "daf6dc47aa4b44791372d6139ab7b25269184d55",
        "type": "github"
      },
      "original": {
        "owner": "NixOS",
        "ref": "nixos-25.05",
        "repo": "nixpkgs",
        "type": "github"
      }
    },
    "nixpkgs_3": {
      "locked": {
        "lastModified": 1710272261,
        "narHash": "sha256-g0bDwXFmTE7uGDOs9HcJsfLFhH7fOsASbAuOzDC+fhQ=",
        "owner": "NixOS",
        "repo": "nixpkgs",
        "rev": "0ad13a6833440b8e238947e47bea7f11071dc2b2",
        "type": "github"
      },
      "original": {
        "owner": "NixOS",
        "ref": "nixos-unstable",
        "repo": "nixpkgs",
        "type": "github"
      }
    },
    "nixpkgs_4": {
      "locked": {
        "lastModified": 1757068644,
        "narHash": "sha256-NOrUtIhTkIIumj1E/Rsv1J37Yi3xGStISEo8tZm3KW4=",
        "owner": "NixOS",
        "repo": "nixpkgs",
        "rev": "8eb28adfa3dc4de28e792e3bf49fcf9007ca8ac9",
        "type": "github"
      },
      "original": {
        "owner": "NixOS",
        "ref": "nixos-unstable",
        "repo": "nixpkgs",
        "type": "github"
      }
    },
    "nixpkgs_5": {
      "locked": {
        "lastModified": 1755615617,
        "narHash": "sha256-HMwfAJBdrr8wXAkbGhtcby1zGFvs+StOp19xNsbqdOg=",
        "owner": "nixos",
        "repo": "nixpkgs",
        "rev": "20075955deac2583bb12f07151c2df830ef346b4",
        "type": "github"
      },
      "original": {
        "owner": "nixos",
        "ref": "nixos-unstable",
        "repo": "nixpkgs",
        "type": "github"
      }
    },
    "nixvirt": {
      "inputs": {
        "nixpkgs": [
          "nixpkgs"
        ]
      },
      "locked": {
        "lastModified": 1748140003,
        "narHash": "sha256-DNBZmuk1YRM2PmwbHzVdXumRjCUzQkMarg4iI/37rOQ=",
        "rev": "5dfe108fd859b122f9a96981cb6bc12297653d6c",
        "revCount": 407,
        "type": "tarball",
        "url": "https://api.flakehub.com/f/pinned/AshleyYakeley/NixVirt/0.6.0/0197059a-e45f-7446-86b5-411ccc894ab0/source.tar.gz"
      },
      "original": {
        "type": "tarball",
        "url": "https://flakehub.com/f/AshleyYakeley/NixVirt/%2A.tar.gz"
      }
    },
    "pre-commit-hooks": {
      "inputs": {
        "flake-compat": "flake-compat",
        "gitignore": "gitignore",
        "nixpkgs": [
          "nixpkgs"
        ]
      },
      "locked": {
        "lastModified": 1760663237,
        "narHash": "sha256-BflA6U4AM1bzuRMR8QqzPXqh8sWVCNDzOdsxXEguJIc=",
        "owner": "cachix",
        "repo": "git-hooks.nix",
        "rev": "ca5b894d3e3e151ffc1db040b6ce4dcc75d31c37",
        "type": "github"
      },
      "original": {
        "owner": "cachix",
        "repo": "git-hooks.nix",
        "type": "github"
      }
    },
    "root": {
      "inputs": {
        "disko": "disko",
        "elephant": "elephant",
        "hardware": "hardware",
        "home-manager": "home-manager",
        "nix-index-database": "nix-index-database",
        "nixpkgs": "nixpkgs_2",
        "nixpkgs-stable": "nixpkgs-stable",
        "nixpkgs-unstable": "nixpkgs-unstable",
        "nixvirt": "nixvirt",
        "pre-commit-hooks": "pre-commit-hooks",
        "rose-pine-hyprcursor": "rose-pine-hyprcursor",
        "spicetify-nix": "spicetify-nix",
        "walker": "walker",
        "zen-browser": "zen-browser"
      }
    },
    "rose-pine-hyprcursor": {
      "inputs": {
        "hyprlang": "hyprlang",
        "nixpkgs": "nixpkgs_3",
        "utils": "utils"
      },
      "locked": {
        "lastModified": 1748096947,
        "narHash": "sha256-ouuA8LVBXzrbYwPW2vNjh7fC9H2UBud/1tUiIM5vPvM=",
        "owner": "ndom91",
        "repo": "rose-pine-hyprcursor",
        "rev": "4b02963d0baf0bee18725cf7c5762b3b3c1392f1",
        "type": "github"
      },
      "original": {
        "owner": "ndom91",
        "repo": "rose-pine-hyprcursor",
        "type": "github"
      }
    },
    "spicetify-nix": {
      "inputs": {
        "nixpkgs": [
          "nixpkgs"
        ],
        "systems": "systems_4"
      },
      "locked": {
        "lastModified": 1761452941,
        "narHash": "sha256-yy+9lSj40cWS4awLqjQ5H5/7/SOf9ZarOgTzH8GHkRk=",
        "owner": "Gerg-L",
        "repo": "spicetify-nix",
        "rev": "20a56cfc4dc794ade2e8d4346cc4a5adcd1bb512",
        "type": "github"
      },
      "original": {
        "owner": "Gerg-L",
        "repo": "spicetify-nix",
        "type": "github"
      }
    },
    "systems": {
      "locked": {
        "lastModified": 1689347949,
        "narHash": "sha256-12tWmuL2zgBgZkdoB6qXZsgJEH9LR3oUgpaQq2RbI80=",
        "owner": "nix-systems",
        "repo": "default-linux",
        "rev": "31732fcf5e8fea42e59c2488ad31a0e651500f68",
        "type": "github"
      },
      "original": {
        "owner": "nix-systems",
        "repo": "default-linux",
        "type": "github"
      }
    },
    "systems_2": {
      "locked": {
        "lastModified": 1689347949,
        "narHash": "sha256-12tWmuL2zgBgZkdoB6qXZsgJEH9LR3oUgpaQq2RbI80=",
        "owner": "nix-systems",
        "repo": "default-linux",
        "rev": "31732fcf5e8fea42e59c2488ad31a0e651500f68",
        "type": "github"
      },
      "original": {
        "owner": "nix-systems",
        "repo": "default-linux",
        "type": "github"
      }
    },
    "systems_3": {
      "locked": {
        "lastModified": 1681028828,
        "narHash": "sha256-Vy1rq5AaRuLzOxct8nz4T6wlgyUR7zLU309k9mBC768=",
        "owner": "nix-systems",
        "repo": "default",
        "rev": "da67096a3b9bf56a91d16901293e51ba5b49a27e",
        "type": "github"
      },
      "original": {
        "owner": "nix-systems",
        "repo": "default",
        "type": "github"
      }
    },
    "systems_4": {
      "locked": {
        "lastModified": 1681028828,
        "narHash": "sha256-Vy1rq5AaRuLzOxct8nz4T6wlgyUR7zLU309k9mBC768=",
        "owner": "nix-systems",
        "repo": "default",
        "rev": "da67096a3b9bf56a91d16901293e51ba5b49a27e",
        "type": "github"
      },
      "original": {
        "owner": "nix-systems",
        "repo": "default",
        "type": "github"
      }
    },
    "systems_5": {
      "locked": {
        "lastModified": 1689347949,
        "narHash": "sha256-12tWmuL2zgBgZkdoB6qXZsgJEH9LR3oUgpaQq2RbI80=",
        "owner": "nix-systems",
        "repo": "default-linux",
        "rev": "31732fcf5e8fea42e59c2488ad31a0e651500f68",
        "type": "github"
      },
      "original": {
        "owner": "nix-systems",
        "repo": "default-linux",
        "type": "github"
      }
    },
    "utils": {
      "inputs": {
        "systems": "systems_3"
      },
      "locked": {
        "lastModified": 1710146030,
        "narHash": "sha256-SZ5L6eA7HJ/nmkzGG7/ISclqe6oZdOZTNoesiInkXPQ=",
        "owner": "numtide",
        "repo": "flake-utils",
        "rev": "b1d9ab70662946ef0850d488da1c9019f3a9752a",
        "type": "github"
      },
      "original": {
        "owner": "numtide",
        "repo": "flake-utils",
        "type": "github"
      }
    },
    "walker": {
      "inputs": {
        "elephant": [
          "elephant"
        ],
        "nixpkgs": "nixpkgs_4",
        "systems": "systems_5"
      },
      "locked": {
        "lastModified": 1762001133,
        "narHash": "sha256-3/93O/z4dcW5URn5FYm6qgYBKqTemCjcyHJ9u5/sEEI=",
        "owner": "abenz1267",
        "repo": "walker",
        "rev": "2058de17f1b973b7691d566c462fd517538f0f51",
        "type": "github"
      },
      "original": {
        "owner": "abenz1267",
        "repo": "walker",
        "type": "github"
      }
    },
    "zen-browser": {
      "inputs": {
        "home-manager": "home-manager_2",
        "nixpkgs": "nixpkgs_5"
      },
      "locked": {
        "lastModified": 1761970869,
        "narHash": "sha256-dlcxJTY+MwR9u3yTkBkzqfHY2+MKonDS7UzoOLlGpuw=",
        "owner": "0xc000022070",
        "repo": "zen-browser-flake",
        "rev": "5855db6427f25672d7f02e19af418a38fe82e26e",
        "type": "github"
      },
      "original": {
        "owner": "0xc000022070",
        "repo": "zen-browser-flake",
        "type": "github"
      }
    }
  },
  "root": "root",
  "version": 7
}
</file>

<file path="flake.nix">
{
  description = "antoniosarro.dev nix config";
  inputs = {
    # ===============================
    # NixOS and Home Manager sources
    # ===============================

    nixpkgs.url = "github:NixOS/nixpkgs/nixos-25.05";
    nixpkgs-stable.url = "github:NixOS/nixpkgs/nixos-25.05";
    nixpkgs-unstable.url = "github:NixOS/nixpkgs/nixos-unstable";

    home-manager = {
      url = "github:nix-community/home-manager/release-25.05";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    # ===============================
    # Utilities
    # ===============================

    # Collection of NixOS modules covering hardware quirks
    hardware.url = "github:nixos/nixos-hardware";

    nix-index-database = {
      url = "github:nix-community/nix-index-database";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    # Declarative partitioning and formatting
    disko = {
      url = "github:nix-community/disko";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    # Pre commit hooks
    pre-commit-hooks = {
      url = "github:cachix/git-hooks.nix";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    # Declarative vms using libvirt
    nixvirt = {
      url = "https://flakehub.com/f/AshleyYakeley/NixVirt/*.tar.gz";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    # ===============================
    # Apps
    # ===============================

    zen-browser = {
      url = "github:0xc000022070/zen-browser-flake";
    };

    # ===============================
    # Theming
    # ===============================

    elephant.url = "github:abenz1267/elephant";
    walker = {
      url = "github:abenz1267/walker";
      inputs.elephant.follows = "elephant";
    };

    rose-pine-hyprcursor.url = "github:ndom91/rose-pine-hyprcursor";

    # Spotify customization
    spicetify-nix = {
      url = "github:Gerg-L/spicetify-nix";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };
  outputs =
    {
      self,
      nixpkgs,
      ...
    }@inputs:
    let
      inherit (self) outputs;

      # ===============================
      # Architectures
      # ===============================
      architectures = nixpkgs.lib.genAttrs [
        "x86_64-linux"
      ];

      # Extend lib with lib.custom
      lib = nixpkgs.lib.extend (self: super: { custom = import ./lib { inherit (nixpkgs) lib; }; });
    in
    {
      # ===============================
      # Overlays
      # ===============================
      overlays = import ./overlays { inherit inputs; };

      # ===============================
      # Host Configurations
      # ===============================
      nixosConfigurations = builtins.listToAttrs (
        map (host: {
          name = host;
          value = nixpkgs.lib.nixosSystem {
            specialArgs = {
              inherit inputs outputs lib;
            };
            modules = [ ./hosts/nixos/${host} ];
          };
        }) (builtins.attrNames (builtins.readDir ./hosts/nixos))
      );

      packages = architectures (
        system:
        let
          pkgs = import nixpkgs {
            inherit system;
            overlays = [ self.overlays.default ];
          };
        in
        nixpkgs.lib.packagesFromDirectoryRecursive {
          callPackage = nixpkgs.lib.callPackageWith pkgs;
          directory = ./pkgs/common;
        }
      );

      # ===============================
      # Formatting
      # ===============================
      formatter = architectures (system: nixpkgs.legacyPackages.${system}.nixfmt-rfc-style);

      checks = architectures (
        system:
        let
          pkgs = nixpkgs.legacyPackages.${system};
        in
        import ./checks.nix { inherit inputs system pkgs; }
      );

      # ===============================
      # Dev Shell
      # ===============================
      devShells = architectures (
        system:
        import ./shell.nix {
          pkgs = nixpkgs.legacyPackages.${system};
          checks = self.checks.${system};
        }
      );
    };
}
</file>

<file path="shell.nix">
{
  pkgs ?
    let
      lock = (builtins.fromJSON (builtins.readFile ./flake.lock)).nodes.nixpkgs.locked;
      nixpkgs = fetchTarball {
        url = "https://github.com/nixos/nixpkgs/archive/${lock.rev}.tar.gz";
        sha256 = lock.narHash;
      };
    in
    import nixpkgs { overlays = [ ]; },
  checks,
  ...
}:
{
  default = pkgs.mkShell {
    NIX_CONFIG = "extra-experimental-features = nix-command flakes";

    inherit (checks.pre-commit-check) shellHook;
    buildInputs = checks.pre-commit-check.enabledPackages;

    nativeBuildInputs = builtins.attrValues {
      inherit (pkgs)
        nix
        home-manager
        nh
        git
        just
        pre-commit
        deadnix
        yq-go
        bats
        ;
    };
  };
}
</file>

</files>
